/* -------------------------------- */
//	GoldenDunk Game Mode			//
//	Author: steeffeen				//
//	Contact: schteffens@gmail.com	//
/* -------------------------------- */

#Extends "Modes/ShootMania/ModeBase.Script.txt"

#Const	CompatibleMapTypes	"GoldenDunkArena"
#Const	Version				"0.6.1 (2013-02-03)"

/* ToDo:
- command post capture percentage callback
*/

/* INCLUDES */
#Include "MathLib" as MathLib
#Include "TextLib" as TextLib
#Include "Libs/Nadeo/Color.Script.txt" as Color
#Include "Libs/Nadeo/Interface.Script.txt" as Interface
#Include "Libs/Nadeo/Top.Script.txt" as Top
#Include "Libs/Nadeo/Victory.Script.txt" as Victory
#Include "Libs/Nadeo/ShootMania/Achievements.Script.txt" as Achievements
#Include "Libs/Nadeo/ShootMania/Airshot.Script.txt" as Airshot
#Include "Libs/Nadeo/ShootMania/BalancedWeapons.Script.txt" as BalancedWeapons
#Include "Libs/Nadeo/ShootMania/Rules.Script.txt" as Rules
#Include "Libs/Nadeo/ShootMania/Score.Script.txt" as Score
#Include "Libs/Nadeo/ShootMania/SM.Script.txt" as SM

/* SETTINGS */
// Round settings
#Setting	S_TimeLimit			240		as _("Time limit per round (seconds)")
#Setting	S_RoundsToWin		3		as _("Rounds to win a map")
#Setting	S_RoundGapToWin		1		as _("Rounds gap to win a map")
#Setting	S_RoundsLimit		7		as _("Total rounds limit per map (First team reaching X wins)")
// Team settings
#Setting	S_AutoTeamBalance	False	as _("Automatically balance teams before each map")
#Setting	S_MaxPlayerCount	0		as _("Maximal count of players (0 = unlimited)")
#Setting	S_NbBotsBlue		0		as _("Number of bots in team Blue")
#Setting	S_NbBotsRed			0		as _("Number of bots in team Red")
#Setting	S_BotPower			2.0		as _("Defines the power of bots (Default: 2.0)")
// Gameplay settings
#Setting	S_PlayersPerBall	3		as _("Number of players for one additional ball")
#Setting	S_FriendlyFire		False	as _("Friendly fire (Damage team members)")
#Setting	S_LaserBattle		False	as _("Laser Battle (Rail gun for every player)")

/* CONSTANTS */
#Const	C_LogEnabled				False		// Enable/disable logging
// Gameplay constants
#Const	C_RespawnTime				3000		// Time until a player is respawned
#Const	C_BallPickUpDistance		2.1			// Distance to pick up balls
#Const	C_PointsForScoring			3			// Points granted if a player scores
#Const	C_CommandPostCaptureTime	30000		// Time to fully capture a command post
#Const	C_CommPostActivationRatio	0.75		// Needed ratio to activate a command post
#Const	C_BallResetTime				5000		// Time until a dropped ball will reset
#Const	C_DecrScoreOnFriendHit		True		// Decrease score on friendly fire hit
#Const	C_NormalExtraAmmoGain		0.0			// Additional ammunition gain when running normal battle
#Const	C_InstaGibExtraAmmoGain		0.5			// Additional ammunition gain when running instagib battle
#Const	C_LongestInstaPassDist		100.0		// Longest possible pass in instagib (0 = no limit, rockets fly up to 150m)
#Const	C_NucleusSelfDamage			False		// Nucleus explosions damage yourself (like a granate)
#Const	C_OnlyHighlightBallCarriers	False		// Enable to highlight ball carriers only (otherwise all players)
#Const	C_BotsCanHaveBall			True		// Disable to disallow bots to have balls
// UI constants
#Const	C_UITickInterval			250			// Interval of UI refreshes
#Const	C_LongUITickInterval		10000		// Interval of UI refreshes of not often updating content
#Const	C_UseTops					True		// Enable/disable top x rankings
#Const	C_TopCount					5			// Count of players displayed in top rankings
#Const	C_UseNewTopStyle			True		// True: Use new top style (Beta2), False: Use old style
#Const	C_ShowBotsInTops			False		// Enable to show bots in top x rankings
#Const	C_EndRoundTopsRight			True		// Location of top rankings at end of rounds (True: Right, False: Top)
#Const	C_HideStuffAtMapEnd			True		// Enable to hide top rankings & stuff at map end
#Const	C_TeamsLayerAtTop			False		// Shows playerlist (if enabled) at the top of the screen
#Const	C_UseAchievements			True		// Enable/disable achievements (may spam your screen!)
#Const	C_UseAirshot				False		// Enable/disable airshot announcement (included in achievements)
#Const	C_UsePositionLayer			True		// Enable/disable position layer (current ranking at bottom right)
#Const	C_UseTeamsLayer				True		// Enable/disable teams layer (playerlists on the sides)
#Const	C_UseSimpleTeamsLayer		False		// Enable/disable simple teams layer (playercounts at the top)
#Const	C_NbTeamHitMessage			3			// Times the team hit warning appears (0: deactivated, -1: endless)
#Const	C_NearMissDist				1.0			// Max distance (meters) for near miss messages (Range: 0.0-1.0)
// Clublink constants
#Const	C_ShowClublinkLayer			True		// Disable to hide the clublink layer (team names)
#Const	C_UsePlayerClublinks		True		// Enable use of player clublinks
#Const	C_NeutralEmblemUrl			""			// Neutral emblem URL

/* TEXTS */
#Const	T_Ball				_("Ball")										// Ball text
#Const	T_Balls				_("Balls")										// Balls text
#Const	T_GoldenGoal		_("Golden Dunk")								// Golden dunk text
#Const	T_CommandPost		_("Command Post")								// Command post text
#Const	T_Constructing		_("Constructing")								// Constructing text
#Const	T_Destroying		_("Destroying")									// Destroying text
#Const	T_Active			_("Active!")									// Active text
#Const	T_Destroyed			_("Destroyed!")									// Destroyed text
#Const	T_WaitForSpawn		_("Waiting to be spawned...")					// Waiting for spawn message
#Const	T_TeamHitMessage	_("$f81Attention! You shot a team member!")		// Warning when hitting a team member
// Top ranking names
#Const	T_Damage			_("Damage")
#Const	T_Scores			_("Scores")
#Const	T_Backstabber		_("Backstabber")

/* GLOBALES */
declare Integer G_LastUIUpdate;					// Time when the last UI refresh was performed
declare Integer G_LastLongUIUpdate;				// Time when the last long UI refresh was performed
declare Integer G_LastBigMessage;				// Time when the last BigMessage was send
declare Integer G_LastStatusMessage;			// Time when the last StatusMessage was send
declare Integer G_BallsCount;					// Number of available balls
declare Integer[Integer] G_ClanPoints;			// Current score of each clan
declare Ident[] G_BallsPolesIDs;				// IDs of the balls poles
declare Ident[Integer] G_CommandPostIDs;		// IDs of the command post poles
declare Text[Integer] G_CommandPostMarkers;		// Markers for the command posts
declare Ident[] G_ActiveCommandPostIDs;			// IDs of active command posts
declare Integer[Ident] G_BallsPerPole;			// Number of balls per pole
declare Ident[Integer] G_ClanSpawnIDs;			// BlockSpawn IDs of each clan
declare Real[Integer] G_HoopCenterXPositions;	// Hoop position of each clan
declare Real[Integer] G_HoopCenterZPositions;	// Hoop position of each clan
declare Text[Integer] G_ClanDefendMarkers;		// Defend markers of each clan
declare Text[Integer] G_ClanAttackMarkers;		// Attack markers of each clan
declare Ident[] G_BallCarryingPlayerIDs;		// IDs of all players that have a ball
declare Ident[] G_PlayingPlayerIDs;				// IDs of all playing players
declare Ident[] G_SpawnedPlayerIDs;				// IDs of all players that are allowed to spawn
declare Ident[] G_WaitingPlayerIDs;				// IDs of all players waiting to be spawned
declare Ident G_ClublinkLayerId;				// Id of the clublink layer
declare Vec3[] G_DroppedBallsPosis;				// Positions of dropped balls
declare Integer[Vec3] G_DroppedBallsTimes;		// Drop times of droppped balls
declare Real[Integer] G_MarkerYs;				// Y coordinate of marker strings
declare CUILayer G_LayerTeamInformation;		// Layer for team information
// Variables caches
declare Boolean Cache_S_FriendlyFire;			// Cache for S_FriendlyFire
declare Boolean Cache_S_LaserBattle;			// Cache for S_LaserBattle
declare Text Cache_XmlRpc_ExtraRules;			// Cache for ExtraRules from server controllers

/*****************************************************
	EXTENSIONS
*****************************************************/

***InitServer***
***
MB_UseLogging = C_LogEnabled;
***

***StartServer***
***
log("GoldenDunk loaded!");
log("Version: "^Version);
log("Written by steeffeen.");

UpdateRules();

Color::Load();

// Teams
UseClans = True;

// Clublinks
MB_UsePlayerClublinks = C_UsePlayerClublinks;
MB_NeutralEmblemUrl = C_NeutralEmblemUrl;

// Tops
if (C_UseTops) {
	Top::AddTop(T_Damage, C_TopCount);
	Top::AddTop(T_Scores, C_TopCount);
	Top::AddTop(T_Backstabber, C_TopCount);
}

// Init variables
MB_UseSectionRound = True;
G_LastUIUpdate = 0;
G_LastLongUIUpdate = 0;
G_LastBigMessage = 0;
G_LastStatusMessage = 0;

Mode::Ladder_OpenMatch_All();

// Achievements
if (C_UseAchievements) {
	Achievements::InitSettings([
	//	"HitRocket",
	//	"HitRail",
	//	"HitNucleus",
	//	"PrecisionRocket",
	//	"PrecisionRail",
	//	"PrecisionNucleus",
		"LongestRail",
		"LongestRocket",
		"LongestAirShot",
		"AirStrike",
	//	"HitUntouched",
	//	"ComboHit",
	//	"LongestTime",
	//	"LessHited",
	//	"DistanceTravel",
	//	"TimeNearEnnemies",
	//	"AirTime",
	//	"ArmorReload",
		"HitAfterHited"
	]);
}
***

***StartMap***
***
XmlRpc.SendCallback("beginMap", fixGetRankings());

ParsePositionsString();

// UI
UIManager.ResetAll();
Rules::Attach();
UIManager.UIAll.BigMessage = "";
UIManager.UIAll.StatusMessage = "";
UIManager.UIAll.Hud3dMarkers = "";
UIManager.UIAll.ManialinkPage = "";
UIManager.UIAll.ScoreTableVisibility = CUIConfig::EVisibility::Normal;
UIManager.UIAll.NoticesFilter_HideMapWarning = True;
// Clan scores at the top
UIManager.UIAll.OverlayScoreSummary = True;
// Name plates
SM::SetupDefaultVisibility();

UIManager.UIAll.SendNotice(
	_("New Match"), CUIConfig::ENoticeLevel::MatchInfo, 
	Null, CUIConfig::EAvatarVariant::Default, 
	CUIConfig::EUISound::StartMatch, 0);
// Position layer
declare LayerPosition <=> UIManager.UILayerCreate();
LayerPosition.AttachId = "LayerPosition";
if (C_UsePositionLayer) {
	Interface::SetLayerPosition(LayerPosition);
}
// Teams layer
declare LayerTeams <=> UIManager.UILayerCreate();
LayerTeams.AttachId = "LayerTeams";
if (C_UseTeamsLayer) {
	if (C_TeamsLayerAtTop) {
		LayerTeams.ManialinkPage = """
			<playerlist posn="-107 90 999" halign="left" valign="top" style="" substyle="Small" lines="5" columns="2" team="1" status="Playing"/>
			<playerlist posn="107 90 999" halign="right" valign="top" style="" substyle="Small" lines="5" columns="2" team="2" status="Playing"/>""";
	} else {
		Interface::SetLayerTeams(LayerTeams);
	}
	UIManager.UIAll.UILayers.add(LayerTeams);
}
// Simple teams layer
declare SimpleLayerTeams <=> UIManager.UILayerCreate();
SimpleLayerTeams.AttachId = "SimpleLayerTeams";
if (C_UseSimpleTeamsLayer) {
	SimpleLayerTeams.ManialinkPage = GetSimpleTeamsLayer();
	UIManager.UIAll.UILayers.add(SimpleLayerTeams);
}
// Top rankings layer
declare LayerTops <=> UIManager.UILayerCreate();
LayerTops.AttachId = "LayerTops";
UIManager.UIAll.UILayers.add(LayerTops);
// Team information layer
G_LayerTeamInformation <=> UIManager.UILayerCreate();
G_LayerTeamInformation.AttachId = "LayerTeamInformation";
UIManager.UIAll.UILayers.add(G_LayerTeamInformation);

// Clublink layer
if (UIManager.UIAll.UILayers.existskey(GetClublinkLayerId())) {
	UIManager.UIAll.UILayers[GetClublinkLayerId()].IsVisible = C_ShowClublinkLayer;
}

// Auto team balance
XmlRpc.SendCallback("TeamBalance", "");
if (S_AutoTeamBalance) {
	Mode::AutoTeamBalance();
	SM::UnspawnAllPlayers();
}

// Prepare match
Score::MatchBegin();
Top::MatchBegin();
Top::RoundBegin();
Victory::MatchBegin();
BalancedWeapons::MatchBegin();
if (C_UseAirshot) {
	Airshot::MatchBegin();
}

G_ClanPoints[1] = 0;
G_ClanPoints[2] = 0;
ClanScores[1] = 0;
ClanScores[2] = 0;
G_BallsCount = 2;

// Prepare map
G_ClanSpawnIDs.clear();
G_BallsPolesIDs.clear();
G_BallsPerPole.clear();
G_MarkerYs.clear();
G_CommandPostIDs.clear();
G_CommandPostMarkers.clear();
G_ActiveCommandPostIDs.clear();
foreach (Base in Bases) {
	Base.IsActive = True;
	Base.Clan = 0;
}
foreach (Index => Spawn in BlockSpawns) {
	G_ClanSpawnIDs[Spawn.Order] = Spawn.Id;
}
foreach (Index => Pole in BlockPoles) {
	switch (Pole.Order) {
		case 0: {
			// Ball spawn
			G_BallsPolesIDs.add(Pole.Id);
			G_BallsPerPole[Pole.Id] = 0;
			Pole.Gauge.ValueReal = 1.0;
			continue;
		}
	}
	switch (Pole.Tag) {
		case "Command Post": {
			if (Pole.Order == 1 || Pole.Order == 2) {
				G_CommandPostIDs[Pole.Order] = Pole.Id;
				Pole.Gauge.Max = C_CommandPostCaptureTime;
			}
		}
	}
}

// Cache settings
Cache_S_FriendlyFire = S_FriendlyFire;
Cache_S_LaserBattle = S_LaserBattle;
UpdateRules();
***

***StartRound***
***
XmlRpc.SendCallback("beginRound", fixGetRankings());

Score::RoundBegin();
Victory::RoundBegin();
BalancedWeapons::RoundBegin();
if (C_UseAirshot) {
	Airshot::RoundBegin();
}

// UI
UIManager.UIAll.ScoreTableVisibility = CUIConfig::EVisibility::Normal;
UIManager.UIAll.SendNotice(
	"", CUIConfig::ENoticeLevel::MatchInfo, 
	Null, CUIConfig::EAvatarVariant::Default, 
	CUIConfig::EUISound::StartRound, 0);

// Initialize values
G_ClanPoints[1] = 0;
G_ClanPoints[2] = 0;
G_BallCarryingPlayerIDs.clear();
G_DroppedBallsPosis.clear();
G_DroppedBallsTimes.clear();
AssignSpawnPoleClans();

// Set bots
SetNbFakePlayers(S_NbBotsBlue, S_NbBotsRed);

// Set round times
StartTime = Now + C_RespawnTime + 1000;
UIManager.UIAll.CountdownEndTime = StartTime + S_TimeLimit * 1000;

UIManager.UIAll.UISequence = CUIConfig::EUISequence::Playing;
***

***OnNewPlayer***
***
// Prepare UI for the new player
declare UI <=> UIManager.GetUI(Player);
if (UI != Null) {
	UI.UILayers.clear();
	if (LayerPosition != Null) {
		UI.UILayers.add(LayerPosition);
	}
}
***

***OnNewSpectator***
***
// Prepare UI for the new spectator
declare UI <=> UIManager.GetUI(Spectator);
if (UI != Null) {
	UI.UILayers.clear();
	
	// Clear crosshair extension
	UI.ManialinkPage = "";
	
	// Spawn queue
	declare LastSpawnTime for Spectator = -1;
	if (LastSpawnTime >= 0) {
		LastSpawnTime = -1;
		if (UI.StatusMessage == T_WaitForSpawn) {
			UI.StatusMessage = "";
		}
	}
}
***

***PlayLoop***
***
// Library updates
if (C_UseAchievements) {
	Achievements::OnLoop();
}
if (C_UseAirshot) {
	Airshot::Update();
}

// Update playing players
G_PlayingPlayerIDs.clear();
foreach (Player in Players) {
	declare LastSpawnTime for Player = -1;
	if (LastSpawnTime >= 0) {
		G_PlayingPlayerIDs.add(Player.Id);
	}
}

// Update waiting players
foreach (PlayerId in G_WaitingPlayerIDs) {
	if (!Players.existskey(PlayerId)) {
		declare Temp = G_WaitingPlayerIDs.remove(PlayerId);
	}
}

// Update ball carriers
foreach (PlayerId in G_BallCarryingPlayerIDs) {
	if (!Players.existskey(PlayerId)) {
		DropBall(PlayerId, True);
	}
}

// Check for changed settings
declare UpdateRules = False;
if (Cache_S_FriendlyFire != S_FriendlyFire) {
	// FriendlyFire changed
	if (S_FriendlyFire) {
		UIManager.UIAll.SendChat("$<$f80Friendly Fire$> is now $<$0f0activated$>! (Pay attention to not damage your team mates!)");
	} else {
		UIManager.UIAll.SendChat("$<$f80Friendly Fire$> is now $<$f11deactivated$>!");
	}
	Cache_S_FriendlyFire = S_FriendlyFire;
	UpdateRules = True;
}
if (Cache_S_LaserBattle != S_LaserBattle) {
	// InstaGib changed
	if (S_LaserBattle) {
		UIManager.UIAll.SendChat("$<$f80LaserBattle$> is now $<$0f0activated$>! (Players spawn with lasers.)");
	} else {
		UIManager.UIAll.SendChat("$<$f80LaserBattle$> is now $<$f11deactivated$>!");
	}
	Cache_S_LaserBattle = S_LaserBattle;
	UpdateRules = True;
}
declare ExtraRules for XmlRpc = "";
if (Cache_XmlRpc_ExtraRules != ExtraRules) {
	Cache_XmlRpc_ExtraRules = ExtraRules;
	UpdateRules = True;
}
if (UpdateRules) {
	UpdateRules();
}

// Perform actions for each player
foreach (Player in Players) {
	// Highlighting
	Player.IsHighlighted = !C_OnlyHighlightBallCarriers && Player.Armor > 0 || C_OnlyHighlightBallCarriers && G_BallCarryingPlayerIDs.exists(Player.Id);

	// Actions
	switch (Player.SpawnStatus) {
		case CSmPlayer::ESpawnStatus::NotSpawned: {
			// Player not spawned -> Spawn him!
			if (!Player.RequestsSpectate) {
				MySpawnPlayer(Player);
			}
		}
		case CSmPlayer::ESpawnStatus::Spawned: {
			// Check for respawn requests
			if (Player.CurrentClan != Player.RequestedClan || Player.RequestsSpectate) {
				DropBall(Player.Id, False);
				UnspawnPlayer(Player);
			} else {
				if (G_BallCarryingPlayerIDs.exists(Player.Id)) {
					// Ball carrier!
				} else {
					// Check if the player picks up a ball
					if (Player.Armor > 0) {
						// At ball spawn
						foreach (PoleId in G_BallsPolesIDs) {
							if (G_BallsPerPole[PoleId] > 0) {
								if (MathLib::Distance(Player.Position, BlockPoles[PoleId].Position) <= C_BallPickUpDistance) {
									declare PickUp = PickUpBall(Player.Id);
									if (PickUp) {
										// Player picks up the ball
										G_BallsPerPole[PoleId] -= 1;
									}
								}
							}
						}
						if (!G_BallCarryingPlayerIDs.exists(Player.Id)) {
							// At dropped balls
							foreach (BallPosi in G_DroppedBallsPosis) {
								if (MathLib::Distance(Player.Position, BallPosi) <= C_BallPickUpDistance) {
									// Player picks up the ball
									if (PickUpBall(Player.Id)) {
										declare Temp = G_DroppedBallsPosis.remove(BallPosi);
									}
								}
							}
						}
					}
				}
			}
		}
	}
}

// Handle pending events
foreach (Event in PendingEvents) {
	if (C_UseAchievements) {
		Achievements::OnEvent(Event);
	}
	switch (Event.Type) {
		case CSmModeEvent::EType::OnArmorEmpty: {
			// Check for scoring
			if (CheckScoring(Event.Victim.Id)) {
				// Player scores for his team
				declare DunkingPointsTeam = 2;
				declare DunkingPointsPlayer = C_PointsForScoring;
				// Check for golden goal
				if (Now > UIManager.UIAll.CountdownEndTime) {
					DunkingPointsPlayer += 1;
				}
				G_ClanPoints[Event.Victim.CurrentClan] += DunkingPointsTeam;
				Score::AddPoints(Event.Victim, DunkingPointsPlayer);
				Top::IncrementPlayerPoints(T_Scores, Event.Victim, DunkingPointsPlayer);
				UIManager.UIAll.SendNotice(
					"""$<{{{Event.Victim.Name}}}$> scores for $<{{{Teams[Event.Victim.CurrentClan-1].ColorizedName}}}$>""", CUIConfig::ENoticeLevel::PlayerWarning,
					Null, CUIConfig::EAvatarVariant::Default,
					CUIConfig::EUISound::Capture, 0);
				UpdateClanScoresSummary();
				UpdateMarkerYs(Event.Victim.CurrentClan, Event.Victim.Position.Y);
				
				if (!Event.Victim.IsFakePlayer) {
					XmlRpc.SendCallback("playerScore", Event.Victim.Login);
					// Check for golden goal
					if (Now > UIManager.UIAll.CountdownEndTime) {
						XmlRpc.SendCallback("goldenGoal", Event.Victim.Login);
					}
				}
				if (C_LogEnabled) MB_Log("Player scores for "^DunkingPointsPlayer^" points: "^Event.Victim.Login);
				
				DropBall(Event.Victim.Id, True);
				// Check for command post
				if (!G_CommandPostIDs.existskey(Event.Victim.CurrentClan) || !G_ActiveCommandPostIDs.exists(BlockPoles[G_CommandPostIDs[Event.Victim.CurrentClan]].Id)) {
					// Respawn player
					UnspawnPlayer(Event.Victim);
				}
				Discard(Event);
			} else {
				if (Event.Victim.IsInOffZone) {
					// Player in non-hoop offzone or without a ball
					if (G_CommandPostIDs.existskey(Event.Victim.CurrentClan) && G_ActiveCommandPostIDs.exists(BlockPoles[G_CommandPostIDs[Event.Victim.CurrentClan]].Id)) {
						// It's fine!
						Discard(Event);
					} else {
						// Kill this noob!
						DropBall(Event.Victim.Id, False);
						if (!Event.Victim.IsFakePlayer) {
							XmlRpc.SendCallback("playerDeath", Event.Victim.Login);
						}
						PassOn(Event);
					}
				} else {
					if (Event.Shooter != Null) {
						BalancedWeapons::OnOut(Event.Shooter, Event.Victim);
						XmlRpc.SendCallback("playerKill", "Victim:"^Event.Victim.Login^";Shooter:"^Event.Shooter.Login);
					}
					if (!Event.Victim.IsFakePlayer) {
						XmlRpc.SendCallback("playerDeath", Event.Victim.Login);
					}
					DropBall(Event.Victim.Id, False);
					PassOn(Event);
				}
			}
		}
		case CSmModeEvent::EType::OnHit: {
			if (Event.Shooter == Null || Event.Victim == Null) {
				// Discard buggy event
				Discard(Event);
			} else {
				// Reduce laser damage
				Event.Damage = 100;
				
				// Handle hit
				if (Event.Shooter == Event.Victim) {
					if (C_NucleusSelfDamage && Event.WeaponNum == This.GetWeaponIndex(CSmMode::EWeapon::Nucleus)+1) {
						// Allow nucleus self damage
						declare Points = -1;
						Event.ShooterPoints = Points;
						// Decrease scores
						if (!Event.Shooter.IsFakePlayer || C_ShowBotsInTops) {
							Top::IncrementPlayerPoints(T_Backstabber, Event.Shooter, -Points);
						}
						if (C_DecrScoreOnFriendHit) {
							Score::AddPoints(Event.Shooter, Points);
						}
						PassOn(Event);
					} else {
						// Discard self damage
						Discard(Event);
					}
				} else {
					if (C_UseAirshot) {
						Airshot::OnHit(Event.Shooter, Event.Victim);
					}
					if (Event.Shooter.CurrentClan == Event.Victim.CurrentClan) {
						if (S_FriendlyFire) {
							// Friendly fire hits
							declare Points = -1;
							Event.ShooterPoints = Points;
							// Decrease scores
							if (!Event.Shooter.IsFakePlayer || C_ShowBotsInTops) {
								Top::IncrementPlayerPoints(T_Backstabber, Event.Shooter, -Points);
							}
							if (C_DecrScoreOnFriendHit) {
								Score::AddPoints(Event.Shooter, Points);
							}
							// Kill victim if he hasn't full armor
							if (Event.Victim.Armor < 200) {
								Event.Damage = Event.Victim.Armor;
							}
							// Show team hit warning
							if (C_NbTeamHitMessage != 0) {
								declare UI <=> UIManager.GetUI(Event.Shooter);
								if (UI != Null) {
									declare NbTeamHitMessage for UI = 0;
									if (C_NbTeamHitMessage < 0 || NbTeamHitMessage < C_NbTeamHitMessage) {
										UI.StatusMessage = T_TeamHitMessage;
										NbTeamHitMessage += 1;
										declare LastStatusMessage for UI = 0;
										LastStatusMessage = Now;
									}
								}
							}
							PassOn(Event);
						} else {
							// Discard team hits
							Discard(Event);
						}
					} else {
						// Enemy hit - Calculate points for the hit
						declare Points = 1;
						if (!Event.Shooter.IsFakePlayer) {
							declare Type = "Attack";
							// Check for defend hits
							if (G_BallCarryingPlayerIDs.exists(Event.Victim.Id)) {
								// Defending hit
								Type = "Defend";
								
								// Check distance to hoop for granting more points
								declare VictimPosi = Event.Victim.Position;
								if (MathLib::Abs(G_HoopCenterXPositions[Event.Victim.CurrentClan] - VictimPosi.X) <= 20.0 && MathLib::Abs(G_HoopCenterZPositions[Event.Victim.CurrentClan] - VictimPosi.Z) <= 20.0) {
									Points *= 2;
								}
							}
							XmlRpc.SendCallback("playerHit", "Victim:"^Event.Victim.Login^";Shooter:"^Event.Shooter.Login^";"^Points^";Type:"^Type);
						}
						Event.ShooterPoints = Points;
						Score::AddPoints(Event.Shooter, Points);
						if (!Event.Shooter.IsFakePlayer || C_ShowBotsInTops) {
							Top::IncrementPlayerPoints(T_Damage, Event.Shooter, Points);
						}
						// Kill victim if he hasn't full armor
						if (Event.Victim.Armor < 200) {
							Event.Damage = Event.Victim.Armor;
						}
						PassOn(Event);
					}
				}
				if (C_UseAirshot && Airshot::MustShowMessage(Event.Shooter)) {
					declare UI <=> UIManager.GetUI(Event.Shooter);
					if (UI != Null) {
						UI.SendNotice(
							Airshot::Message(Event.Shooter), CUIConfig::ENoticeLevel::Default,
							Null, CUIConfig::EAvatarVariant::Default,
							CUIConfig::EUISound::Bonus, 0);
					}
				}
			}
		}
		case CSmModeEvent::EType::OnPlayerRequestRespawn: {
			// Drop a ball
			DropBall(Event.Player.Id, False);
			MySpawnPlayer(Event.Player);
			if (!Event.Player.IsFakePlayer) {
				XmlRpc.SendCallback("playerRespawn", Event.Player.Login);
			}
			Discard(Event);
		}
		case CSmModeEvent::EType::OnNearMiss: {
			// Near miss
			if (C_NearMissDist >= 0 && Event.MissDist <= C_NearMissDist) {
				declare UI <=> UIManager.GetUI(Event.Shooter);
				if (UI != Null) {
					UI.StatusMessage = GetNearMissMessage(Event);
					declare LastStatusMessage for UI = 0;
					LastStatusMessage = Now;
				}
			}
			PassOn(Event);
		}
		default: {
			PassOn(Event);
		}
	}
}

// Reset dropped balls
foreach (BallPosi => BallDropTime in G_DroppedBallsTimes) {
	if (BallDropTime + C_BallResetTime < Now) {
		declare Temp = G_DroppedBallsPosis.remove(BallPosi);
		Temp = G_DroppedBallsTimes.removekey(BallPosi);
	}
}
UpdateBallSpawns();

// Check for command post captures
foreach (Clan => PoleId in G_CommandPostIDs) {
	declare Speed = 0;
	foreach (PlayerId in BlockPoles[PoleId].Sector.PlayersIds) {
		if (Players[PlayerId].CurrentClan == Clan) {
			// Mate
			Speed += 1;
		} else {
			// Opponent
			Speed -= 1;
		}
		declare UI <=> UIManager.GetUI(Players[PlayerId]);
		if (UI != Null) {
			UI.GaugeClan = BlockPoles[PoleId].Gauge.Clan;
			UI.GaugeRatio = BlockPoles[PoleId].Gauge.ValueReal;
			if (BlockPoles[PoleId].Gauge.Clan == Players[PlayerId].CurrentClan) {
				// Constructing
				if (G_ActiveCommandPostIDs.exists(BlockPoles[PoleId].Id)) {
					// Command post already fully constructed
					UI.GaugeMessage = T_Active;
				} else {
					UI.GaugeMessage = T_Constructing^"...";
				}
			} else {
				// Destroying
				if (BlockPoles[PoleId].Gauge.ValueReal <= 0.0) {
					// Command post already completely destroyed
					UI.GaugeMessage = T_Destroyed;
				} else {
					UI.GaugeMessage = T_Destroying^"...";
				}
			}
		}
	}
	BlockPoles[PoleId].Gauge.Speed = Speed;
	
	// Check if command post activation status changes
	if (G_ActiveCommandPostIDs.exists(PoleId)) {
		// Command post active
		if (BlockPoles[PoleId].Gauge.ValueReal < C_CommPostActivationRatio) {
			// Command post becomes deactivated
			declare Temp = G_ActiveCommandPostIDs.remove(PoleId);
			UIManager.UIAll.SendNotice(
				"$<"^Teams[BlockPoles[PoleId].Gauge.Clan-1].ColorizedName^"s$> "^T_CommandPost^" was deactived!",
				CUIConfig::ENoticeLevel::MapInfo,
				Null, CUIConfig::EAvatarVariant::Default,
				CUIConfig::EUISound::Checkpoint, 1);
		}
	} else {
		// Command post not active yet
		if (BlockPoles[PoleId].Gauge.ValueReal > C_CommPostActivationRatio) {
			// Command post becomes activated
			G_ActiveCommandPostIDs.add(PoleId);
			UIManager.UIAll.SendNotice(
				"$<"^Teams[BlockPoles[PoleId].Gauge.Clan-1].ColorizedName^"s$> "^T_CommandPost^" was actived!",
				CUIConfig::ENoticeLevel::MapInfo,
				Null, CUIConfig::EAvatarVariant::Default,
				CUIConfig::EUISound::Checkpoint, 0);
		}
	}
}

// UI updates
if (G_LastUIUpdate + C_UITickInterval < Now) {
	G_LastUIUpdate = Now;
	
	// Team information
	UpdateTeamInformationManialink();
	
	// Top rankings
	if (C_UseTops) {
		declare TopNb = 2;
		if (S_FriendlyFire) {
			TopNb += 1;
		}
		declare Position = "129 -30";
		switch (((Now - StartTime) / 7000) % TopNb) {
			case 0: {
				LayerTops.ManialinkPage = GetFrameTop(T_Damage, Position);
			}
			case 1: {
				LayerTops.ManialinkPage = GetFrameTop(T_Scores, Position);
			}
			case 2: {
				LayerTops.ManialinkPage = GetFrameTop(T_Backstabber, Position);
			}
		}
	}
	
	// Clear big messages
	if (G_LastBigMessage > 0 && G_LastBigMessage + 5000 < Now) {
		UIManager.UIAll.BigMessage = "";
		G_LastBigMessage = 0;
	}
	
	// Clear status messages
	if (G_LastStatusMessage > 0 && G_LastStatusMessage + 5000 < Now) {
		UIManager.UIAll.StatusMessage = "";
		G_LastStatusMessage = 0;
	}
	
	// Clan scores at the top
	UpdateClanScoresSummary();
	
	// Build new simple teams layer update (to update team color)
	if (C_UseSimpleTeamsLayer) {
		SimpleLayerTeams.ManialinkPage = GetSimpleTeamsLayer();
	}
	
	// Update players UI
	foreach (Player in Players) {
		declare UI <=> UIManager.GetUI(Player);
		if (UI != Null) {
			// Update layers
			UpdatePositionLayer(Player);
			UpdateSimpleTeamsLayer(Player);
			
			// Update markers
			declare Markers =	GetBallsHud3dMarkers()
								^
								G_ClanAttackMarkers[Player.CurrentClan]
								^
								G_ClanDefendMarkers[Player.CurrentClan];
			if (G_CommandPostMarkers.existskey(1) && G_CommandPostMarkers.existskey(2)) {
				Markers ^=	G_CommandPostMarkers[1]
							^
							G_CommandPostMarkers[2];
			}
			UI.Hud3dMarkers = Markers;
			
			// Gauge
			if (Player.BlockPole == Null) {
				UI.GaugeRatio = -1.0;
				UI.GaugeMessage = "";
			}
			
			// Update crosshair extension
			if (G_BallCarryingPlayerIDs.exists(Player.Id)) {
				UI.ManialinkPage = """<label posn="0 7" halign="center" scale="2" text="{{{Teams[Player.CurrentClan-1].ColorText}}}〝     〞" style="TextCardSmallScores1"/><label posn="0 -8" halign="center" scale="1" text="{{{Teams[Player.CurrentClan-1].ColorText}}}$n$oYou have a {{{T_Ball}}}" style="TextCardSmallScores2"/>""";
			} else {
				UI.ManialinkPage = "";
			}
			
			// Clear player status message
			declare LastStatusMessage for UI = 0;
			if (UI.StatusMessage != "" && (LastStatusMessage > 0 && LastStatusMessage + 4500 < Now) || UI.StatusMessage == T_WaitForSpawn) {
				UI.StatusMessage = "";
				LastStatusMessage = 0;
			}
		}
	}
	
	// Update spectators UI
	foreach (Spectator in Spectators) {
		declare UI <=> UIManager.GetUI(Spectator);
		if (UI != Null) {
			// Update layers
			UpdateSimpleTeamsLayer(Spectator);
			
			// Update markers
			declare Markers = GetBallsHud3dMarkers();
			if (G_CommandPostMarkers.existskey(1) && G_CommandPostMarkers.existskey(2)) {
				Markers ^=	G_CommandPostMarkers[1]
							^
							G_CommandPostMarkers[2];
			}
			UI.Hud3dMarkers = Markers;
			
			// Gauge
			UI.GaugeRatio = -1.0;
			UI.GaugeMessage = "";
		}
	}
}

// Round end conditions
if (Now >= UIManager.UIAll.CountdownEndTime) {
	if (G_ClanPoints[1] == G_ClanPoints[2] && Players.count > 0) {
		// Even scores - Wait for the golden goal!
		declare Message = """$0f0$w$s{{{T_GoldenGoal}}}!""";
		if (UIManager.UIAll.StatusMessage != Message) {
			UIManager.UIAll.StatusMessage = Message;
			UIManager.UIAll.SendNotice(
				"", CUIConfig::ENoticeLevel::MatchInfo,
				Null, CUIConfig::EAvatarVariant::Default,
				CUIConfig::EUISound::PhaseChange, 0);
		}
	} else {
		MB_StopRound = True;
		UIManager.UIAll.StatusMessage = "";
		UIManager.UIAll.SendNotice(
			"", CUIConfig::ENoticeLevel::MatchInfo,
			Null, CUIConfig::EAvatarVariant::Default,
			CUIConfig::EUISound::Warning, 0);
	}
}
***

***EndRound***
***
XmlRpc.SendCallback("endRound", fixGetRankings());

UIManager.UIAll.UISequence = CUIConfig::EUISequence::EndRound;

StartTime = -1;
UIManager.UIAll.CountdownEndTime = -1;
SM::UnspawnAllPlayers();
MB_Sleep(3000);

// Evaluate round
declare Message = _("Round Draw");
for (Index, 1, 2) {
	if (G_ClanPoints[Index] > G_ClanPoints[3-Index]) {
		// Team 'Index' wins the round
		ClanScores[Index] += 1;
		Message = """{{{Teams[Index-1].ColorizedName}}} wins the round!""";
		break;
	}
}
UpdateClanScoresSummary();

UIManager.UIAll.BigMessage = "";
UIManager.UIAll.SendNotice(
	Message, CUIConfig::ENoticeLevel::MatchInfo,
	Null, CUIConfig::EAvatarVariant::Default,
	CUIConfig::EUISound::EndRound, 0);
if (C_LogEnabled) MB_Log(Message);

UIManager.UIAll.ScoreTableVisibility = CUIConfig::EVisibility::ForcedVisible;

// Show all top lists
if (C_UseTops) {
	declare ManialinkPage = "";
	
	if (C_EndRoundTopsRight) {
		ManialinkPage =	GetFrameTop(T_Damage, "129 40")
						^
						GetFrameTop(T_Scores, "129 5");
		if (S_FriendlyFire) {
			ManialinkPage ^= GetFrameTop(T_Backstabber, "-129 -30");
		}
	} else {
		ManialinkPage =	GetFrameTop(T_Damage, "-65 85")
						^
						GetFrameTop(T_Scores, "-5 85");
		if (S_FriendlyFire) {
			ManialinkPage ^= GetFrameTop(T_Backstabber, "55 85");
		}
	}
	
	LayerTops.ManialinkPage = ManialinkPage;
}

// Check match end conditions
Victory::SetMatchWinnerFromScore(S_RoundsToWin, S_RoundGapToWin, S_RoundsLimit);
if (!Victory::NoMatchWinner()) {
	// Match winner found
	MB_StopMap = True;
}

// Wait to let the players take a breath
MB_Sleep(4000);

Score::RoundEnd();
Victory::RoundEnd();
BalancedWeapons::RoundEnd();
if (C_UseAirshot) {
	Airshot::RoundEnd();
}
***

***EndMap***
***
XmlRpc.SendCallback("endMap", fixGetRankings());

Score::MatchEnd();
Top::RoundEnd();
Top::MatchEnd();
Victory::MatchEnd();
BalancedWeapons::MatchEnd();
if (C_UseAirshot) {
	Airshot::MatchEnd();
}

// UI
UIManager.UIAll.BigMessage = "";
UIManager.UIAll.StatusMessage = "";
UIManager.UIAll.Hud3dMarkers = "";
UIManager.UIAll.ManialinkPage = "";
UIManager.UIAll.UISequence = CUIConfig::EUISequence::Podium;

// Hide useless layers
if (C_HideStuffAtMapEnd) {
	// Custom layers
	LayerPosition.IsVisible = !C_HideStuffAtMapEnd;
	LayerTops.IsVisible = !C_HideStuffAtMapEnd;
	LayerTeams.IsVisible = !C_HideStuffAtMapEnd;
	SimpleLayerTeams.IsVisible = !C_HideStuffAtMapEnd;
	G_LayerTeamInformation.IsVisible = !C_HideStuffAtMapEnd;
	// Clublink layer
	if (UIManager.UIAll.UILayers.existskey(GetClublinkLayerId())) {
		UIManager.UIAll.UILayers[GetClublinkLayerId()].IsVisible = False;
	}
}

// Announce victorious clan
declare Message = _("Match Draw");
if (!Victory::IsMatchDraw()) {
	for (Index, 1, 2) {
		if (Victory::IsMatchWinner(Index)) {
			// Team 'Index' wins the match
			Message = """{{{Teams[Index-1].ColorizedName}}} wins the match!""";
			break;
		}
	}
}
UIManager.UIAll.SendNotice(
	Message, CUIConfig::ENoticeLevel::MatchInfo, 
	Null, CUIConfig::EAvatarVariant::Default, 
	CUIConfig::EUISound::EndMatch, 0);
if (C_LogEnabled) MB_Log(Message);

MB_Sleep(4000);

// Save spawned players for the next map
G_SpawnedPlayerIDs.clear();
foreach (Player in Players) {
	declare LastSpawnTime for Player = -1;
	if (LastSpawnTime >= 0) {
		G_SpawnedPlayerIDs.add(Player.Id);
	}
}

// UI Cleanup
Rules::Detach();
UIManager.UILayerDestroy(LayerTeams);
UIManager.UILayerDestroy(SimpleLayerTeams);
UIManager.UILayerDestroy(LayerPosition);
UIManager.UILayerDestroy(LayerTops);
UIManager.UILayerDestroy(G_LayerTeamInformation);
***

***EndServer***
***
Mode::Ladder_CloseMatch();

Rules::Destroy();
Color::Unload();

if (C_UseAchievements) {
	Achievements::ResetTop("all");
}
***

/*****************************************************
	FUNCTIONS
*****************************************************/

// Updates the rules layer
Void UpdateRules() {
	declare ModeName = "GoldenDunk";
	
	declare AvailableBallsText = "";
	if (G_BallsCount > 1) {
		// Multiple balls
		AvailableBallsText = """are $<$f80{{{G_BallsCount}}}$> $<$f0f{{{T_Balls}}}$>""";
	} else {
		// Single ball
		AvailableBallsText = """is $<$f80{{{G_BallsCount}}}$> $<$f0f{{{T_Ball}}}$>""";
	}
	declare ModeRules = """
- Two teams fight for $<$f0f{{{T_Balls}}}$> in order to score at the opponents hoop.
- Grab a $<$f0f{{{T_Ball}}}$> and get it into the enemys hoop to score. (There {{{AvailableBallsText}}} available which can be picked up by any player on any team). You can still hit players while having a $<$f0f{{{T_Ball}}}$>!
- If a player with a $<$f0f{{{T_Ball}}}$> dies it will drop at his position and can be picked up for $<$f80{{{C_BallResetTime/1000.0}}}$> seconds before it resets.
- One round lasts $<$f80{{{S_TimeLimit}}}$> seconds and the team with the most points at the end of the round wins it. If both teams have even scores a $<$0f0{{{T_GoldenGoal}}}$> will decide the round.
""";
	if (G_CommandPostIDs.count > 0) {
		ModeRules ^= """- Capture your teams $<$0f0{{{T_CommandPost}}}$> to be able to walk through offzone. (The effect activates at $<$f80{{{C_CommPostActivationRatio}}}%$>.)
""";
	}
	ModeRules ^= "\n";
	
	// Mode settings hints
	if (S_FriendlyFire) {
		ModeRules ^= """- $<$f80Friendly Fire$> is activated! (Pay attention to not damage your team mates!)
""";
	}
	if (S_LaserBattle) {
		ModeRules ^= """- $<$f80LaserBattle$> is activated! (Players spawn with lasers.)
""";
	}
	
	// XmlRpc extra rules
	declare ExtraRules for XmlRpc = "";
	if (ExtraRules != "") {
		if (S_FriendlyFire || S_LaserBattle) {
			ModeRules ^= "\n";
		}
		ModeRules ^= "$<"^ExtraRules^"$>";
	}
	
	// Create and attach new rules
	Rules::Create(ModeName, ModeRules, False);
	Rules::Attach();
}

// Set ball count of ball spawns
Void UpdateBallSpawns() {
	// Update number of available balls
	declare OldCount = G_BallsCount;
	declare PlayerCount = 0;
	foreach (Player in Players) {
		if (!Player.IsFakePlayer) {
			PlayerCount += 1;
		}
	}
	G_BallsCount = PlayerCount / S_PlayersPerBall;
	if (G_BallsCount < 1) {
		G_BallsCount = 1;
	}
	if (OldCount != G_BallsCount) {
		UpdateRules();
	}
	
	// Set balls to poles
	declare FreeBalls = G_BallsCount - G_DroppedBallsPosis.count - G_BallCarryingPlayerIDs.count;
	foreach (PoleId in G_BallsPolesIDs) {
		FreeBalls -= G_BallsPerPole[PoleId];
	}
	declare BallsCount = 0;
	while (FreeBalls > 0) {
		foreach (PoleId in G_BallsPolesIDs) {
			if (G_BallsPerPole[PoleId] == BallsCount) {
				G_BallsPerPole[PoleId] += 1;
				FreeBalls -= 1;
			}
		}
		BallsCount += 1;
	}
}

// Parse positions string to array
Void ParsePositionsString() {
	declare String = Map.ObjectiveTextAuthor;
	if (C_LogEnabled) MB_Log(String);
	declare Begin = 0;
	declare Order = -1;
	declare XPosi = "-0.0";
	declare ZPosi = "-0.0";
	
	for (Index, 0, TextLib::Length(String)) {
		declare Char = TextLib::SubString(String, Index, 1);
		switch (Char) {
			case ":": {
				Order = TextLib::ToInteger(TextLib::SubString(String, Begin, Index-Begin));
				Begin = Index+1;
			}
			case ",": {
				XPosi = TextLib::SubString(String, Begin, Index-Begin);
				Begin = Index+1;
			}
			case ";": {
				ZPosi = TextLib::SubString(String, Begin, Index-Begin);
				Begin = Index+1;
				
				G_HoopCenterXPositions[Order] = TextLib::ToReal(XPosi) * (376.0/47.0) + 4.0;
				G_HoopCenterZPositions[Order] = TextLib::ToReal(ZPosi) * (376.0/47.0) + 4.0;
			}
		}
	}
}

// Set bot power
Void SetBotPower(CSmPlayer _Player) {
	if (_Player.IsFakePlayer) {
		// Ammunition
		_Player.AmmoGain = S_BotPower;
		
		// Armor
		declare ArmorMax = MathLib::NearestInteger(S_BotPower * 100);
		if (ArmorMax > 1000) {
			ArmorMax = 1000;
		} else {
			if (ArmorMax < 100) {
				ArmorMax = 100;
			}
		}
		_Player.ArmorMax = ArmorMax;
		
		// Agressivity
		_Player.BotAgressivity = S_BotPower;
	}
}

// Spawn player
Void MySpawnPlayer(CSmPlayer _Player) {
	UnspawnPlayer(_Player);
	
	// Check if the player is allowed to spawn
	declare SpawnAllowed = False;
	declare LastSpawnTime for _Player = -1;
	if (S_MaxPlayerCount <= 0 || LastSpawnTime >= 0 || G_SpawnedPlayerIDs.exists(_Player.Id)) {
		// Player has already been spawned
		SpawnAllowed = True;
	} else {
		if (G_PlayingPlayerIDs.count < S_MaxPlayerCount && G_WaitingPlayerIDs.count > 0 && G_WaitingPlayerIDs[0] == _Player.Id) {
			// Player is the longest waiting one
			SpawnAllowed = True;
		}
	}
	
	if (SpawnAllowed) {
		// Set bot power
		SetBotPower(_Player);
		
		// Set player weapon
		BalancedWeapons::SetPlayerWeapon(_Player, True);
		if (S_LaserBattle) {
			This.SetPlayerWeapon(_Player, CSmMode::EWeapon::Laser, False);
			_Player.AmmoGain += C_InstaGibExtraAmmoGain;
		} else {
			This.SetPlayerWeapon(_Player, CSmMode::EWeapon::Rocket, True);
			_Player.AmmoGain += C_NormalExtraAmmoGain;
		}
		
		// Calculate spawn time
		declare SpawnTime = StartTime;
		if (Now > SpawnTime) {
			SpawnTime = Now + C_RespawnTime;
		}
		LastSpawnTime = SpawnTime;
		
		if (!G_PlayingPlayerIDs.exists(_Player.Id)) {
			G_PlayingPlayerIDs.add(_Player.Id);
		}
		SM::SpawnPlayer(_Player, _Player.RequestedClan, BlockSpawns[G_ClanSpawnIDs[_Player.RequestedClan]], SpawnTime);
		if (C_LogEnabled) MB_Log("""Spawning player: {{{_Player.Login}}}""");
		
		// Remove player from waiting queue
		if (G_WaitingPlayerIDs.exists(_Player.Id)) {
			declare Temp = G_WaitingPlayerIDs.remove(_Player.Id);
		}
	} else {
		// Add player to waiting queue
		if (!G_WaitingPlayerIDs.exists(_Player.Id)) {
			G_WaitingPlayerIDs.add(_Player.Id);
		}
		
		// Display waiting message
		declare UI <=> UIManager.GetUI(_Player);
		if (UI != Null) {
			if (UI.StatusMessage == "") {
				UI.StatusMessage = T_WaitForSpawn;
			}
		}
	}
}

// Updates attack/defend marker strings
Void UpdateMarkerStrings() {
	for (Index, 1, 2) {
		declare Y = 40.0;
		if (G_MarkerYs.existskey(Index)) {
			Y = G_MarkerYs[Index];
		}
		declare HoopPosi = <G_HoopCenterXPositions[Index], Y, G_HoopCenterZPositions[Index]>;
		G_ClanDefendMarkers[3-Index] = """<marker label="{{{Teams[Index-1].ColorText^_("Defend")}}}" pos="{{{HoopPosi[0]}}} {{{HoopPosi[1]}}} {{{HoopPosi[2]}}}"/>""";
		G_ClanAttackMarkers[Index] = """<marker label="{{{Teams[Index-1].ColorText^_("Target")}}}" pos="{{{HoopPosi[0]}}} {{{HoopPosi[1]}}} {{{HoopPosi[2]}}}"/>""";
	}
	
	foreach (Order => PoleId in G_CommandPostIDs) {
		declare PolePosi = BlockPoles[PoleId].Position;
		G_CommandPostMarkers[Order] = """<marker label="{{{Teams[Order-1].ColorText^T_CommandPost}}}" pos="{{{PolePosi[0]}}} {{{PolePosi[1]}}} {{{PolePosi[2]}}}"/>""";
	}
}

// Updates marker Ys
Void UpdateMarkerYs(Integer _Index, Real _Y) {
	if (!G_MarkerYs.existskey(_Index)) {
		// Y not set yet
		G_MarkerYs[_Index] = _Y + 5.0;
		UpdateMarkerStrings();
	} else {
		// Y already set, check for update
		if (G_MarkerYs[_Index] + 5.0 < _Y) {
			G_MarkerYs[_Index] = _Y + 5.0;
			UpdateMarkerStrings();
		}
	}
}

// Set clans of spawns and poles at start of map and each round (changing sides)
Void AssignSpawnPoleClans() {
	if (C_LogEnabled) MB_Log("AssignSpawnPoleClans");
	
	// SPAWNS
	// Change sides
	declare FirstSpawns = G_ClanSpawnIDs[1];
	G_ClanSpawnIDs[1] = G_ClanSpawnIDs[2];
	G_ClanSpawnIDs[2] = FirstSpawns;
	// Assign clans
	foreach (Clan => SpawnId in G_ClanSpawnIDs) {
		switch (BlockSpawns[SpawnId].Base.Clan) {
			case 0: {
				BlockSpawns[SpawnId].Base.Clan = Clan;
			}
			default: {
				BlockSpawns[SpawnId].Base.Clan = 3-BlockSpawns[SpawnId].Base.Clan;
			}
		}
	}
	
	// HOOPS
	// Change sides
	declare OldX = G_HoopCenterXPositions[1];
	declare OldZ = G_HoopCenterZPositions[1];
	G_HoopCenterXPositions[1] = G_HoopCenterXPositions[2];
	G_HoopCenterZPositions[1] = G_HoopCenterZPositions[2];
	G_HoopCenterXPositions[2] = OldX;
	G_HoopCenterZPositions[2] = OldZ;
	if (G_MarkerYs.existskey(1) && G_MarkerYs.existskey(2)) {
		declare OldY = G_MarkerYs[1];
		G_MarkerYs[1] = G_MarkerYs[2];
		G_MarkerYs[2] = OldY;
	}
	
	// COMMAND POSTS
	if (G_CommandPostIDs.existskey(1) && G_CommandPostIDs.existskey(2)) {
		// Change sides
		declare OldId = G_CommandPostIDs[1];
		G_CommandPostIDs[1] = G_CommandPostIDs[2];
		G_CommandPostIDs[2] = OldId;
		// Assign clans
		foreach (Clan => PoleId in G_CommandPostIDs) {
			// Bases
			switch (BlockPoles[PoleId].Base.Clan) {
				case 0: {
					BlockPoles[PoleId].Base.Clan = Clan;
				}
				default: {
					BlockPoles[PoleId].Base.Clan = 3-BlockPoles[PoleId].Base.Clan;
				}
			}
			// Gauges
			switch (BlockPoles[PoleId].Gauge.Clan) {
				case 0: {
					BlockPoles[PoleId].Gauge.Clan = Clan;
				}
				default: {
					BlockPoles[PoleId].Gauge.Clan = 3-BlockPoles[PoleId].Gauge.Clan;
				}
			}
			// Initialize values
			BlockPoles[PoleId].Gauge.Speed = 0;
			BlockPoles[PoleId].Gauge.ValueReal = 0.0;
			BlockPoles[PoleId].Captured = False;
		}
	}
	
	// Prepare markers
	UpdateMarkerStrings();
}

// Player picks up a ball
Boolean PickUpBall(Ident _PlayerId) {
	// Perform several checks if picking up is allowed right now because of weird bugs of the game itself
	if (Players.existskey(_PlayerId) && !G_BallCarryingPlayerIDs.exists(_PlayerId) && G_BallCarryingPlayerIDs.count < G_BallsCount && Now >= StartTime && Players[_PlayerId].Armor > 0 && (C_BotsCanHaveBall || !Players[_PlayerId].IsFakePlayer)) {
		G_BallCarryingPlayerIDs.add(_PlayerId);
		Score::AddPoints(Players[_PlayerId], 1);
		declare UI <=> UIManager.GetUI(Players[_PlayerId]);
		if (UI != Null) {
			UI.SendNotice(
				"", CUIConfig::ENoticeLevel::PlayerInfo, 
				Null, CUIConfig::EAvatarVariant::Default, 
				CUIConfig::EUISound::FirstHit, 0);
		}
		if (C_LogEnabled) MB_Log("""{{{Players[_PlayerId].Login}}} picked up a {{{T_Ball}}}!""");
		return True;
	}
	return False;
}

// Check if a player is able to score
Boolean CheckScoring(Ident _PlayerId) {
	// Check ball carrying and offzone
	if (Players.existskey(_PlayerId) && G_BallCarryingPlayerIDs.exists(_PlayerId) && Players[_PlayerId].IsInOffZone) {
		// Check distance to hoop
		declare PlayerPosi = Players[_PlayerId].Position;
		if (MathLib::Abs(G_HoopCenterXPositions[Players[_PlayerId].CurrentClan] - PlayerPosi[0]) <= 4.0 && MathLib::Abs(G_HoopCenterZPositions[Players[_PlayerId].CurrentClan] - PlayerPosi[2]) <= 4.0) {
			return True;
		}
	}
	
	return False;
}

// Ball carrier drops a ball
Void DropBall(Ident _PlayerId, Boolean _Reset) {
	if (G_BallCarryingPlayerIDs.exists(_PlayerId)) {
		// Remove ball carrier
		declare Temp = G_BallCarryingPlayerIDs.remove(_PlayerId);
		
		// Save position of dropped ball
		if (!_Reset && Players.existskey(_PlayerId)) {
			G_DroppedBallsPosis.add(Players[_PlayerId].Position);
			G_DroppedBallsTimes[Players[_PlayerId].Position] = Now;
		}
	}
}

// Updates clan scores at the top
Void UpdateClanScoresSummary() {
	declare PlayerClan1Id = NullId;
	declare PlayerClan2Id = NullId;
	foreach (Player in Players) {
		if (PlayerClan1Id == NullId && Player.CurrentClan == 1) {
			PlayerClan1Id = Player.Id;
		} else {
			if (PlayerClan2Id == NullId && Player.CurrentClan == 2) {
				PlayerClan2Id = Player.Id;
			} else {
				if (PlayerClan1Id != NullId && PlayerClan2Id != NullId) {
					break;
				}
			}
		}
	}
	if (PlayerClan1Id != NullId && PlayerClan2Id != NullId) {
		UIManager.UIAll.OverlayScoreSummary = True;
		UIManager.UIAll.ScoreSummary_Player1 = PlayerClan1Id;
		UIManager.UIAll.ScoreSummary_Points1 = G_ClanPoints[Players[PlayerClan1Id].CurrentClan];
		UIManager.UIAll.ScoreSummary_MatchPoints1 = ClanScores[Players[PlayerClan1Id].CurrentClan];
		UIManager.UIAll.ScoreSummary_Player2 = PlayerClan2Id;
		UIManager.UIAll.ScoreSummary_Points2 = G_ClanPoints[Players[PlayerClan2Id].CurrentClan];
		UIManager.UIAll.ScoreSummary_MatchPoints2 = ClanScores[Players[PlayerClan2Id].CurrentClan];
	} else {
		UIManager.UIAll.OverlayScoreSummary = False;
	}
}

// Get top frame depending on style setting
Text GetFrameTop(Text _TopIdent, Text _Pos) {
	declare Title = """Top {{{C_TopCount}}} {{{_TopIdent}}}""";
	declare UnitType = "";
	switch (_TopIdent) {
		case T_Damage: {
		
		}
		case T_Scores: {
		
		}
		case T_Backstabber: {
			Title = """Flop {{{C_TopCount}}} {{{_TopIdent}}}""";
		}
	}
	if (C_UseNewTopStyle) {
		return Top::GetFrameTop2(_TopIdent, Title, _Pos, UnitType);
	}
	return Top::GetFrameTop(_TopIdent, Title, _Pos, UnitType);
}

// Create simple teams layer manialink
Text GetSimpleTeamsLayer() {
	return """
		<script><!--
			main() {
				declare Label_Blue <=> (Page.GetFirstChild("BlueLabel") as CMlLabel);
				declare Label_Red <=> (Page.GetFirstChild("RedLabel") as CMlLabel);
				declare netread Net_NbPlayersBlue for UI = 0;
				declare netread Net_ColorBlue for UI = "";
				declare netread Net_NbPlayersRed for UI = 0;
				declare netread Net_ColorRed for UI = "";
				
				while (True) {
					yield;
					
					// Update display of player counts
					// Blue
					declare LabelText = Net_NbPlayersBlue^" Player";
					if (Net_NbPlayersBlue > 1) {
						LabelText ^= "s";
					}
					Label_Blue.SetText(LabelText);
					
					// Red
					LabelText = Net_NbPlayersRed^" Player";
					if (Net_NbPlayersRed > 1) {
						LabelText ^= "s";
					}
					Label_Red.SetText(LabelText);
				}
			}
		--></script>
		<frame>
			// Blue team
			<quad posn="-32 90.2 0" sizen="15 3" halign="center" valign="top" bgcolor="{{{Color::RgbToHex(Teams[0].ColorPrimary)}}}7"/>
			<label posn="-32 90.0 1" scale="0.6" halign="center" valign="top" id="BlueLabel"/>
			
			// Red team
			<quad posn="32.5 90.2 0" sizen="15 3" halign="center" valign="top" bgcolor="{{{Color::RgbToHex(Teams[1].ColorPrimary)}}}7"/>
			<label posn="32.5 90.0 1" scale="0.6" halign="center" valign="top" id="RedLabel"/>
		</frame>""";
}

// Update simple teams layer
Void UpdateSimpleTeamsLayer(CSmPlayer _Player) {
	if (C_UseSimpleTeamsLayer) {
		declare UI <=> UIManager.GetUI(_Player);
		if (UI != Null) {
			declare netwrite Net_NbPlayersBlue for UI = 0;
			declare netwrite Net_NbPlayersRed for UI = 0;
			
			Net_NbPlayersBlue = ClansNbPlayersAlive[1];
			Net_NbPlayersRed = ClansNbPlayersAlive[2];
		}
	}
}

// Update player position layer
Void UpdatePositionLayer(CSmPlayer _Player) {
	if (C_UsePositionLayer) {
		declare UI <=> UIManager.GetUI(_Player);
		if (UI != Null) {
			declare netwrite Net_LibInterface_LayerInfoUpdate for UI = 0;
			declare netwrite Net_LibInterface_PosTotal for UI = 0;
			declare netwrite Net_LibInterface_PosCurrent for UI = 0;
			
			Net_LibInterface_LayerInfoUpdate = Now;
			Net_LibInterface_PosTotal = Scores.count;
			Net_LibInterface_PosCurrent = Scores.keyof(_Player.Score) + 1;
		}
	}
}

// Calculates remaining drop time of balls
Integer GetRemainingDropTime(Vec3 _BallPosi) {
	if (G_DroppedBallsPosis.exists(_BallPosi)) {
		declare DropTime = G_DroppedBallsTimes[_BallPosi];
		return (DropTime + C_BallResetTime - Now) / 1000;
	}
	return -1;
}

// Generates Hud3dMarkers for all balls
Text GetBallsHud3dMarkers() {
	declare Markers = "";
	
	// Balls on ball spawn
	if (G_BallCarryingPlayerIDs.count + G_DroppedBallsPosis.count < G_BallsCount) {
		foreach (PoleId in G_BallsPolesIDs) {
			declare Posi = BlockPoles[PoleId].Position;
			declare BallText = "$0f0";
			if (G_BallsPerPole[PoleId] > 1) {
				// Multiple balls left
				BallText ^= G_BallsPerPole[PoleId] ^" "^ T_Balls;
			} else {
				// Only one ball left
				BallText ^= T_Ball;
			}
			Markers ^= """<marker label="$<{{{BallText}}}$>" pos="{{{Posi[0]}}} {{{Posi[1]}}} {{{Posi[2]}}}"/>""";
		}
	}
	
	// Balls carried by players
	foreach (PlayerId in G_BallCarryingPlayerIDs) {
		if (Players.existskey(PlayerId)) {
			if (Players[PlayerId].IsFakePlayer) {
				// Bot
				Markers ^= """<marker label="$<{{{Teams[Players[PlayerId].CurrentClan-1].ColorText}}}{{{T_Ball}}}$>" pos="{{{Players[PlayerId].Position[0]}}} {{{Players[PlayerId].Position[1]}}} {{{Players[PlayerId].Position[2]}}}"/>""";
			} else {
				// Normal player
				Markers ^= """<marker label="$<{{{Teams[Players[PlayerId].CurrentClan-1].ColorText}}}{{{T_Ball}}}$>" playerlogin="{{{Players[PlayerId].Login}}}"/>""";
			}
		}
	}
	
	// Balls dropped elsewhere
	foreach (BallPosi in G_DroppedBallsPosis) {
		declare RemText = "";
		declare RemTime = GetRemainingDropTime(BallPosi);
		if (RemTime >= 0) {
			RemText = """ ({{{RemTime}}})""";
		}
		Markers ^= """<marker label="$<$0f0{{{T_Ball}}}{{{RemText}}}$>" pos="{{{BallPosi[0]}}} {{{BallPosi[1]}}} {{{BallPosi[2]}}}"/>""";
	}
	
	return Markers;
}

// Creates message for near miss
Text GetNearMissMessage(CSmModeEvent _Event) {
	// Catch invalid events
	if (_Event.Type != CSmModeEvent::EType::OnNearMiss) return "";	// Wrong event type
	if (_Event.MissDist <= 0.0) return "";							// Invalid miss distance
	
	// Get message text
	declare Message = "";
	declare Dist = MathLib::NearestInteger(_Event.MissDist * 1000);
	if (Dist >= 10) {
		Message = """You missed $<{{{_Event.Victim.Name}}}$> by {{{Dist/10.0}}}cm!""";
	} else {
		Dist = MathLib::NearestInteger(_Event.MissDist * 10000);
		if (Dist >= 10) {
			Message = """You missed $<{{{_Event.Victim.Name}}}$> by {{{Dist/10.0}}}mm!""";
		} else {
			Dist = MathLib::NearestInteger(_Event.MissDist * 10000000);
			Message = """You missed $<{{{_Event.Victim.Name}}}$> by {{{Dist/10.0}}}µm!""";
		}
	}
	return Message;
}

// Get the clublink layer
Ident GetClublinkLayerId() {
	if (!UIManager.UIAll.UILayers.existskey(G_ClublinkLayerId)) {
		G_ClublinkLayerId = NullId;
	}
	if (G_ClublinkLayerId == NullId) {
		foreach (Layer in UIManager.UIAll.UILayers) {
			switch (Layer.AttachId) {
				case "Unassigned": {
					// Unknown layer
					switch (Layer.Type) {
						case CUILayer::EUILayerType::ScreenIn3d: {
							// Rules layer
							Layer.AttachId = "LayerRules";
						}
						case CUILayer::EUILayerType::Normal: {
							// Clublink layer
							G_ClublinkLayerId = Layer.Id;
							break;
						}
						default: {
							// Other layers
						}
					}
				}
				default: {
					// Known layer
				}
			}
		}
	}
	return G_ClublinkLayerId;
}

// Generates the manialink with all the information about the teams (balls & command posts)
Void UpdateTeamInformationManialink() {
	// Balls
	declare BlueCount = 0;
	declare RedCount = 0;
	foreach (PlayerId in G_BallCarryingPlayerIDs) {
		if (Players.existskey(PlayerId)) {
			if (Players[PlayerId].CurrentClan == 1) {
				BlueCount += 1;
			} else {
				RedCount += 1;
			}
		}
	}
	
	// Poles
	declare BluePole = "";
	if (G_CommandPostIDs.existskey(1)) {
		if (G_ActiveCommandPostIDs.exists(G_CommandPostIDs[1])) {
			// Active command post
			BluePole = "http://dl.dropbox.com/u/58886549/ManiaPlanet/Images/bluepole_v1.png";
		} else {
			// Inactive command post
			BluePole = "http://dl.dropbox.com/u/58886549/ManiaPlanet/Images/greypole_v1.png";
		}
	}
	declare RedPole = "";
	if (G_CommandPostIDs.existskey(2)) {
		if (G_ActiveCommandPostIDs.exists(G_CommandPostIDs[2])) {
			// Active command post
			RedPole = "http://dl.dropbox.com/u/58886549/ManiaPlanet/Images/redpole_v1.png";
		} else {
			// Inactive command post
			RedPole = "http://dl.dropbox.com/u/58886549/ManiaPlanet/Images/greypole_v1.png";
		}
	}
	
	G_LayerTeamInformation.ManialinkPage = """
	<frame posn="110 -72" halign="center" valign="center">
	<!--	<label posn="-1 9" scale="1.3" halign="right" valign="center" text="$00f$oBlue"/>
		<label posn="1 9" scale="1.3" halign="left" valign="center" text="$f00$oRed"/>	-->
		
		<quad posn="-8 0" sizen="8 8" halign="center" valign="center" image="http://dl.dropbox.com/u/58886549/ManiaPlanet/Images/ball_v1.png"/>
		<label posn="-7.5 0.5" sizen="8 8" scale="1.3" halign="center" valign="center" text="$00f$o{{{BlueCount}}}"/>
		<quad posn="8 0" sizen="8 8" halign="center" valign="center" image="http://dl.dropbox.com/u/58886549/ManiaPlanet/Images/ball_v1.png"/>
		<label posn="8.5 0.5" sizen="8 8" scale="1.3" halign="center" valign="center" text="$f00$o{{{RedCount}}}"/>
		
		<quad posn="-8 -10" sizen="8 8" halign="center" valign="center" image="{{{BluePole}}}"/>
		<quad posn="8 -10" sizen="8 8" halign="center" valign="center" image="{{{RedPole}}}"/>
	</frame>""";
}

// Get the current rankings for xmlrpc callbacks
Text fixGetRankings() {
	declare PlayerList = "";
	foreach (Score in Scores) {
		PlayerList ^= Score.User.Login^":"^Score.Points^";";
	}
	return PlayerList;
}
