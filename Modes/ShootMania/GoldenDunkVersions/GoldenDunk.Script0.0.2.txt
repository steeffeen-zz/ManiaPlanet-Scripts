/* ------------------------ */
//	GoldenDunk Game Mode	//
//	Created by steeffeen	//
/* ------------------------ */

#Extends "Modes/ShootMania/ModeBase.Script.txt"

#Const	CompatibleMapTypes	"GoldenDunkArena,SpeedBallArena,SpeedBall2Arena"
#Const	Version				"0.0.2 (2013-01-31)"

/* INCLUDES */
#Include "MathLib" as MathLib
#Include "TextLib" as TextLib
#Include "Libs/Nadeo/Color.Script.txt" as Color
#Include "Libs/Nadeo/Interface.Script.txt" as Interface
#Include "Libs/Nadeo/Top.Script.txt" as Top
#Include "Libs/Nadeo/Victory.Script.txt" as Victory
#Include "Libs/Nadeo/ShootMania/Achievements.Script.txt" as Achievements
#Include "Libs/Nadeo/ShootMania/Airshot.Script.txt" as Airshot
#Include "Libs/Nadeo/ShootMania/BalancedWeapons.Script.txt" as BalancedWeapons
#Include "Libs/Nadeo/ShootMania/Rules.Script.txt" as Rules
#Include "Libs/Nadeo/ShootMania/Score.Script.txt" as Score
#Include "Libs/Nadeo/ShootMania/SM.Script.txt" as SM

/* SETTINGS */
// Round settings
#Setting	S_TimeLimit			240		as _("Time limit per round (seconds)")
#Setting	S_RoundsToWin		3		as _("Rounds to win a map")
#Setting	S_RoundGapToWin		1		as _("Rounds gap to win a map")
#Setting	S_RoundsLimit		7		as _("Total rounds limit per map (First team reaching X wins)")
// Team settings
#Setting	S_AutoTeamBalance	False	as _("Automatically balance teams before each map")
#Setting	S_MaxPlayerCount	0		as _("Maximal count of players (0 = unlimited)")
#Setting	S_NbBotsBlue		0		as _("Number of bots in team Blue")
#Setting	S_NbBotsRed			0		as _("Number of bots in team Red")
#Setting	S_BotPower			2.0		as _("Defines the power of bots (Default: 2.0)")
// Gameplay settings
#Setting	S_NbBalls			3		as _("Number of available Balls")
#Setting	S_FriendlyFire		False	as _("Friendly fire (Damage team members)")
#Setting	S_LaserBattle		False	as _("Laser Battle (Rail gun for every player)")

/* CONSTANTS */
#Const	C_LogEnabled				False		// Enable/disable logging
// Gameplay constants
#Const	C_RespawnTime				3000		// Time until a player is respawned
#Const	C_BallPickUpDistance		2.2			// Distance to pick up balls
#Const	C_PointsForScoring			3			// Points granted if a player scores
#Const	C_BallResetTime				10000		// Time until a dropped ball will reset
#Const	C_DecrScoreOnFriendHit		True		// Decrease score on friendly fire hit
#Const	C_NormalAmmoPower			1.0			// Ammunition power when running normal battle
#Const	C_InstaGibAmmoPower			0.5			// Ammunition power when running instagib battle
#Const	C_NormalExtraAmmoGain		0.0			// Additional ammunition gain when running normal battle
#Const	C_InstaGibExtraAmmoGain		0.5			// Additional ammunition gain when running instagib battle
#Const	C_LongestInstaPassDist		100.0		// Longest possible pass in instagib (0 = no limit, rockets fly up to 150m)
#Const	C_NucleusSelfDamage			False		// Nucleus explosions damage yourself (like a granate)
#Const	C_OnlyHighlightBallCarriers	True		// Enable to highlight ball carriers only (otherwise all players)
#Const	C_BotsCanHaveBall			True		// Disable to disallow bots to have balls
// UI constants
#Const	C_UITickInterval			250			// Interval of UI refreshes
#Const	C_LongUITickInterval		10000		// Interval of UI refreshes of not often updating content
#Const	C_UseTops					True		// Enable/disable top x rankings
#Const	C_TopCount					5			// Count of players displayed in top rankings
#Const	C_UseNewTopStyle			True		// True: Use new top style (Beta2), False: Use old style
#Const	C_ShowBotsInTops			False		// Enable to show bots in top x rankings
#Const	C_EndRoundTopsRight			True		// Location of top rankings at end of rounds (True: Right, False: Top)
#Const	C_HideStuffAtMapEnd			True		// Enable to hide top rankings & stuff at map end
#Const	C_TeamsLayerAtTop			False		// Shows playerlist (if enabled) at the top of the screen
#Const	C_UseAchievements			True		// Enable/disable achievements (may spam your screen!)
#Const	C_UseAirshot				False		// Enable/disable airshot announcement
#Const	C_UsePositionLayer			True		// Enable/disable position layer (current ranking at bottom right)
#Const	C_UseTeamsLayer				True		// Enable/disable teams layer (playerlists on the sides)
#Const	C_UseSimpleTeamsLayer		False		// Enable/disable simple teams layer (playercounts at the top)
#Const	C_NbTeamHitMessage			3			// Times the team hit warning appears (0: deactivated, -1: endless)
#Const	C_NearMissDist				1.0			// Max distance (meters) for near miss messages (Range: 0.0-1.0)
// Clublink constants
#Const	C_ShowClublinkLayer			True		// Disable to hide the clublink layer (team names)
#Const	C_UsePlayerClublinks		True		// Enable use of player clublinks
#Const	C_NeutralEmblemUrl			""			// Neutral emblem URL

/* TEXTS */

#Const	T_Ball				_("Ball")										// Ball text
#Const	T_Balls				_("Balls")										// Balls text
#Const	T_WaitForSpawn		_("Waiting to be spawned...")					// Waiting for spawn message
#Const	T_TeamHitMessage	_("$f81Attention! You shot a team member!")		// Warning when hitting a team member
// Top ranking names
#Const	T_Damage			_("Damage")
#Const	T_Scores			_("Scores")
#Const	T_Backstabber		_("Backstabber")

/* GLOBALES */
declare Integer G_LastUIUpdate;					// Time when the last UI refresh was performed
declare Integer G_LastLongUIUpdate;				// Time when the last long UI refresh was performed
declare Integer G_LastBigMessage;				// Time when the last BigMessage was send
declare Integer G_LastStatusMessage;			// Time when the last StatusMessage was send
declare Integer[Integer] G_ClanPoints;			// Current score of each clan
declare Ident G_BallsPoleId;					// Id of the ball pole
declare Ident[Integer] G_ClanSpawnIDs;			// BlockSpawn IDs of each clan
declare Ident[Integer] G_ClanPoleIDs;			// BlockPole IDs of each clan
declare Text[Integer] G_ClanDefendMarkers;		// Defend markers of each clan
declare Text[Integer] G_ClanAttackMarkers;		// Attack markers of each clan
declare Ident[] G_BallCarryingPlayerIDs;		// IDs of all players that have a ball
declare Ident[] G_PlayingPlayerIDs;				// IDs of all playing players
declare Ident[] G_SpawnedPlayerIDs;				// IDs of all players that are allowed to spawn
declare Ident[] G_WaitingPlayerIDs;				// IDs of all players waiting to be spawned
declare Ident G_ClublinkLayerId;				// Id of the clublink layer
// Setting caches
declare Boolean Cache_S_FriendlyFire;			// Cache for S_FriendlyFire
declare Boolean Cache_S_LaserBattle;			// Cache for S_LaserBattle

/*****************************************************
	EXTENSIONS
*****************************************************/
***Rules***
***
// Create rules
declare ModeName = "GoldenDunk";

declare ModeRules = """
- Two teams fight for $<$f0fBalls$> in order to score at the opponents hoop.
- Grab a $<$f0fBall$> and get it into the enemys hoop to score. (There are $<$f80{{{S_NbBalls}}}$> $<$f0fBalls$> available which can be picked up by any player on any team).
- One round lasts $<$f80{{{S_TimeLimit}}}$> seconds and the team with the most points at the end of the round wins it. If both teams have even scores a $<$0f0Golden Goal$> will decide the round.
""";
if (S_FriendlyFire) {
	ModeRules ^= """
- $<$f80Friendly Fire$> is activated! Pay attention to not damage your team mates!""";
}
if (S_LaserBattle) {
	ModeRules ^= """
- $<$f80LaserBattle$> is activated! Players spawn with lasers! (Hits cause still only 1 damage.)""";
}

Rules::Create(ModeName, ModeRules, False);

// Attach rules
Rules::Attach();
***

***InitServer***
***
MB_UseLogging = C_LogEnabled;
***

***StartServer***
***
log("GoldenDunk loaded!");
log("Version: "^Version);
log("Written by steeffeen.");

---Rules---

Color::Load();

// Teams
UseClans = True;

// Clublinks
MB_UsePlayerClublinks = C_UsePlayerClublinks;
MB_NeutralEmblemUrl = C_NeutralEmblemUrl;

// Tops
if (C_UseTops) {
	Top::AddTop(T_Damage, C_TopCount);
	Top::AddTop(T_Scores, C_TopCount);
	Top::AddTop(T_Backstabber, C_TopCount);
}

// Init variables
MB_UseSectionRound = True;
G_LastUIUpdate = 0;
G_LastLongUIUpdate = 0;
G_LastBigMessage = 0;
G_LastStatusMessage = 0;

Mode::Ladder_OpenMatch_All();

// Achievements
if (C_UseAchievements) {
	Achievements::InitSettings([
	//	"HitRocket",
	//	"HitRail",
	//	"HitNucleus",
	//	"PrecisionRocket",
	//	"PrecisionRail",
	//	"PrecisionNucleus",
		"LongestRail",
		"LongestRocket",
		"LongestAirShot",
		"AirStrike",
	//	"HitUntouched",
	//	"ComboHit",
	//	"LongestTime",
	//	"LessHited",
	//	"DistanceTravel",
	//	"TimeNearEnnemies",
	//	"AirTime",
	//	"ArmorReload",
		"HitAfterHited"
	]);
}
***

***StartMap***
***
XmlRpc.SendCallback("beginMap", fixGetRankings());

// UI
UIManager.ResetAll();
Rules::Attach();
UIManager.UIAll.BigMessage = "";
UIManager.UIAll.StatusMessage = "";
UIManager.UIAll.Hud3dMarkers = "";
UIManager.UIAll.ManialinkPage = "";
UIManager.UIAll.ScoreTableVisibility = CUIConfig::EVisibility::Normal;
// Clan scores at the top
UIManager.UIAll.OverlayScoreSummary = True;
// Name plates
SM::SetupDefaultVisibility();

UIManager.UIAll.SendNotice(
	_("New Match"), CUIConfig::ENoticeLevel::MatchInfo, 
	Null, CUIConfig::EAvatarVariant::Default, 
	CUIConfig::EUISound::StartMatch, 0);
// Position layer
declare LayerPosition <=> UIManager.UILayerCreate();
LayerPosition.AttachId = "LayerPosition";
if (C_UsePositionLayer) {
	Interface::SetLayerPosition(LayerPosition);
}
// Teams layer
declare LayerTeams <=> UIManager.UILayerCreate();
LayerTeams.AttachId = "LayerTeams";
if (C_UseTeamsLayer) {
	if (C_TeamsLayerAtTop) {
		LayerTeams.ManialinkPage = """
			<playerlist posn="-107 90 999" halign="left" valign="top" style="" substyle="Small" lines="5" columns="2" team="1" status="Playing"/>
			<playerlist posn="107 90 999" halign="right" valign="top" style="" substyle="Small" lines="5" columns="2" team="2" status="Playing"/>""";
	} else {
		Interface::SetLayerTeams(LayerTeams);
	}
	UIManager.UIAll.UILayers.add(LayerTeams);
}
// Simple teams layer
declare SimpleLayerTeams <=> UIManager.UILayerCreate();
SimpleLayerTeams.AttachId = "SimpleLayerTeams";
if (C_UseSimpleTeamsLayer) {
	SimpleLayerTeams.ManialinkPage = GetSimpleTeamsLayer();
	UIManager.UIAll.UILayers.add(SimpleLayerTeams);
}
// Top rankings layer
declare LayerTops <=> UIManager.UILayerCreate();
LayerTops.AttachId = "LayerTops";
UIManager.UIAll.UILayers.add(LayerTops);

// Clublink layer
if (UIManager.UIAll.UILayers.existskey(GetClublinkLayerId())) {
	UIManager.UIAll.UILayers[GetClublinkLayerId()].IsVisible = C_ShowClublinkLayer;
}

// Auto team balance
if (S_AutoTeamBalance) {
	Mode::AutoTeamBalance();
	SM::UnspawnAllPlayers();
}

// Prepare match
Score::MatchBegin();
Top::MatchBegin();
Top::RoundBegin();
Victory::MatchBegin();
BalancedWeapons::MatchBegin();
if (C_UseAirshot) {
	Airshot::MatchBegin();
}

G_ClanPoints[1] = 0;
G_ClanPoints[2] = 0;
ClanScores[1] = 0;
ClanScores[2] = 0;

// Prepare map
G_ClanSpawnIDs.clear();
G_ClanPoleIDs.clear();
G_BallsPoleId = NullId;
foreach (Base in Bases) {
	Base.IsActive = True;
	Base.Clan = 0;
}
foreach (Index => Spawn in BlockSpawns) {
	G_ClanSpawnIDs[Spawn.Order] = Spawn.Id;
}
foreach (Index => Pole in BlockPoles) {
	switch (Pole.Order) {
		case 3: {
			// Ball spawn
			G_BallsPoleId = Pole.Id;
		}
		default: {
			G_ClanPoleIDs[Pole.Order] = Pole.Id;
		}
	}
	// Design
	Pole.Gauge.ValueReal = 1.0;
}

// Cache settings
Cache_S_FriendlyFire = S_FriendlyFire;
Cache_S_LaserBattle = S_LaserBattle;
---Rules---
***

***StartRound***
***
XmlRpc.SendCallback("beginRound", fixGetRankings());

Score::RoundBegin();
Victory::RoundBegin();
BalancedWeapons::RoundBegin();
if (C_UseAirshot) {
	Airshot::RoundBegin();
}

// UI
UIManager.UIAll.ScoreTableVisibility = CUIConfig::EVisibility::Normal;
UIManager.UIAll.SendNotice(
	"", CUIConfig::ENoticeLevel::MatchInfo, 
	Null, CUIConfig::EAvatarVariant::Default, 
	CUIConfig::EUISound::StartRound, 0);

// Initialize values
G_ClanPoints[1] = 0;
G_ClanPoints[2] = 0;
G_BallCarryingPlayerIDs.clear();
AssignSpawnPoleClans();

// Set bots
SetNbFakePlayers(S_NbBotsBlue, S_NbBotsRed);

// Set round times
StartTime = Now + C_RespawnTime + 1000;
UIManager.UIAll.CountdownEndTime = StartTime + S_TimeLimit * 1000;

UIManager.UIAll.UISequence = CUIConfig::EUISequence::Playing;
***

***OnNewPlayer***
***
// Prepare UI for the new player
declare UI <=> UIManager.GetUI(Player);
if (UI != Null) {
	UI.UILayers.clear();
	if (LayerPosition != Null) {
		UI.UILayers.add(LayerPosition);
	}
}
***

***OnNewSpectator***
***
// Prepare UI for the new spectator
declare UI <=> UIManager.GetUI(Spectator);
if (UI != Null) {
	UI.UILayers.clear();
	
	// Clear crosshair extension
	UI.ManialinkPage = "";
	
	// Spawn queue
	declare LastSpawnTime for Spectator = -1;
	if (LastSpawnTime >= 0) {
		LastSpawnTime = -1;
		if (UI.StatusMessage == T_WaitForSpawn) {
			UI.StatusMessage = "";
		}
	}
}
***

***PlayLoop***
***
// Library updates
if (C_UseAchievements) {
	Achievements::OnLoop();
}
if (C_UseAirshot) {
	Airshot::Update();
}

// Update playing players
G_PlayingPlayerIDs.clear();
foreach (Player in Players) {
	declare LastSpawnTime for Player = -1;
	if (LastSpawnTime >= 0) {
		G_PlayingPlayerIDs.add(Player.Id);
	}
}

// Update waiting players
foreach (PlayerId in G_WaitingPlayerIDs) {
	if (!Players.existskey(PlayerId)) {
		declare Temp = G_WaitingPlayerIDs.remove(PlayerId);
	}
}

// Update ball carriers
foreach (PlayerId in G_BallCarryingPlayerIDs) {
	if (!Players.existskey(PlayerId)) {
		DropBall(PlayerId);
	}
}

// Check for changed settings
declare UpdateRules = False;
if (Cache_S_FriendlyFire != S_FriendlyFire) {
	// FriendlyFire changed
	if (S_FriendlyFire) {
		UIManager.UIAll.SendChat("$<$f80Friendly Fire$> is now $<$0f0activated$>! (Pay attention to not damage your team mates!)");
	} else {
		UIManager.UIAll.SendChat("$<$f80Friendly Fire$> is now $<$f11deactivated$>!");
	}
	Cache_S_FriendlyFire = S_FriendlyFire;
	UpdateRules = True;
}
if (Cache_S_LaserBattle != S_LaserBattle) {
	// InstaGib changed
	if (S_LaserBattle) {
		UIManager.UIAll.SendChat("$<$f80LaserBattle$> is now $<$0f0activated$>! (Players spawn with lasers! Hits cause still 1 damage.)");
	} else {
		UIManager.UIAll.SendChat("$<$f80LaserBattle$> is now $<$f11deactivated$>!");
	}
	Cache_S_LaserBattle = S_LaserBattle;
	UpdateRules = True;
}
if (UpdateRules) {
	---Rules---
}

// Perform actions for each player
foreach (Player in Players) {
	// Highlighting
	Player.IsHighlighted = !C_OnlyHighlightBallCarriers && Player.Armor > 0 || C_OnlyHighlightBallCarriers && G_BallCarryingPlayerIDs.exists(Player.Id);

	// Actions
	switch (Player.SpawnStatus) {
		case CSmPlayer::ESpawnStatus::NotSpawned: {
			// Player not spawned -> Spawn him!
			if (!Player.RequestsSpectate) {
				MySpawnPlayer(Player);
			}
		}
		case CSmPlayer::ESpawnStatus::Spawned: {
			// Check for respawn requests
			if (Player.CurrentClan != Player.RequestedClan || Player.RequestsSpectate) {
				DropBall(Player.Id);
				UnspawnPlayer(Player);
			} else {
				if (G_BallCarryingPlayerIDs.exists(Player.Id)) {
					// Ball carrier!
				} else {
					// Check if the player picks up the ball
					if (Player.Armor > 0) {
						if (MathLib::Distance(Player.Position, BlockPoles[G_BallsPoleId].Position) <= C_BallPickUpDistance) {
							// Player picks up the ball
							PickUpBall(Player.Id);
						}
					}
				}
			}
		}
	}
}

// Handle pending events
foreach (Event in PendingEvents) {
	if (C_UseAchievements) {
		Achievements::OnEvent(Event);
	}
	switch (Event.Type) {
		case CSmModeEvent::EType::OnArmorEmpty: {
			// Check for scoring
			if (CheckScoring(Event.Victim.Id)) {
				// Player scores for his team
				G_ClanPoints[Event.Victim.CurrentClan] += 1;
				Score::AddPoints(Event.Victim, C_PointsForScoring);
				Top::IncrementPlayerPoints(T_Scores, Event.Victim, 1);
				UIManager.UIAll.SendNotice(
					"""$<{{{Event.Victim.Name}}}$> scores for $<{{{Teams[Event.Victim.CurrentClan-1].ColorizedName}}}$>""", CUIConfig::ENoticeLevel::PlayerWarning,
					Null, CUIConfig::EAvatarVariant::Default,
					CUIConfig::EUISound::Capture, 0);
				UpdateClanScoresSummary();
				
				if (!Event.Victim.IsFakePlayer) {
					XmlRpc.SendCallback("playerScore", Event.Victim.Login);
				}
				if (C_LogEnabled) MB_Log("Player scores: "^Event.Victim.Login);
				
				// Respawn player
				DropBall(Event.Victim.Id);
				UnspawnPlayer(Event.Victim);
				
				Discard(Event);
			} else {
				// Drop a ball
				DropBall(Event.Victim.Id);
				
				if (Event.Shooter != Null) {
					BalancedWeapons::OnOut(Event.Shooter, Event.Victim);
					XmlRpc.SendCallback("playerKill", "Victim:"^Event.Victim.Login^";Shooter:"^Event.Shooter.Login);
				}
				if (!Event.Victim.IsFakePlayer) {
					XmlRpc.SendCallback("playerDeath", Event.Victim.Login);
				}
				PassOn(Event);
			}
		}
		case CSmModeEvent::EType::OnHit: {
			if (Event.Shooter == Null || Event.Victim == Null) {
				// Discard buggy event
				Discard(Event);
			} else {
				if (Event.Shooter == Event.Victim) {
					if (C_NucleusSelfDamage && Event.WeaponNum == This.GetWeaponIndex(CSmMode::EWeapon::Nucleus)+1) {
						// Allow nucleus self damage
						declare Points = -1;
						Event.ShooterPoints = Points;
						// Decrease scores
						if (!Event.Shooter.IsFakePlayer || C_ShowBotsInTops) {
							Top::IncrementPlayerPoints(T_Backstabber, Event.Shooter, -Points);
						}
						if (C_DecrScoreOnFriendHit) {
							Score::AddPoints(Event.Shooter, Points);
						}
						PassOn(Event);
					} else {
						// Discard self damage
						Discard(Event);
					}
				} else {
					if (C_UseAirshot) {
						Airshot::OnHit(Event.Shooter, Event.Victim);
					}
					if (Event.Shooter.CurrentClan == Event.Victim.CurrentClan) {
						if (S_FriendlyFire) {
							// Friendly fire hits
							declare Points = -1;
							Event.ShooterPoints = Points;
							// Decrease scores
							if (!Event.Shooter.IsFakePlayer || C_ShowBotsInTops) {
								Top::IncrementPlayerPoints(T_Backstabber, Event.Shooter, -Points);
							}
							if (C_DecrScoreOnFriendHit) {
								Score::AddPoints(Event.Shooter, Points);
							}
							// Kill victim if he hasn't full armor
							if (Event.Victim.Armor < 200) {
								Event.Damage = Event.Victim.Armor;
							}
							// Show team hit warning
							if (C_NbTeamHitMessage != 0) {
								declare UI <=> UIManager.GetUI(Event.Shooter);
								if (UI != Null) {
									declare NbTeamHitMessage for UI = 0;
									if (C_NbTeamHitMessage < 0 || NbTeamHitMessage < C_NbTeamHitMessage) {
										UI.StatusMessage = T_TeamHitMessage;
										NbTeamHitMessage += 1;
										declare LastStatusMessage for UI = 0;
										LastStatusMessage = Now;
									}
								}
							}
							PassOn(Event);
						} else {
							// Discard team hits
							Discard(Event);
						}
					} else {
						// Calculate points for the hit
						declare Points = 1;
						Event.ShooterPoints = Points;
						Score::AddPoints(Event.Shooter, Points);
						if (!Event.Shooter.IsFakePlayer || C_ShowBotsInTops) {
							Top::IncrementPlayerPoints(T_Damage, Event.Shooter, Points);
						}
						// Kill victim if he hasn't full armor
						if (Event.Victim.Armor < 200) {
							Event.Damage = Event.Victim.Armor;
						}
						if (!Event.Shooter.IsFakePlayer) {
							XmlRpc.SendCallback("playerHit", "Victim:"^Event.Victim.Login^";Shooter:"^Event.Shooter.Login^";"^Points);
						}
						PassOn(Event);
					}
				}
				if (C_UseAirshot && Airshot::MustShowMessage(Event.Shooter)) {
					declare UI <=> UIManager.GetUI(Event.Shooter);
					if (UI != Null) {
						UI.SendNotice(
							Airshot::Message(Event.Shooter), CUIConfig::ENoticeLevel::Default,
							Null, CUIConfig::EAvatarVariant::Default,
							CUIConfig::EUISound::Bonus, 0);
					}
				}
			}
		}
		case CSmModeEvent::EType::OnPlayerRequestRespawn: {
			// Drop a ball
			DropBall(Event.Player.Id);
			MySpawnPlayer(Event.Player);
			if (!Event.Player.IsFakePlayer) {
				XmlRpc.SendCallback("playerRespawn", Event.Player.Login);
			}
			Discard(Event);
		}
		case CSmModeEvent::EType::OnNearMiss: {
			// Near miss
			if (C_NearMissDist >= 0 && Event.MissDist <= C_NearMissDist) {
				declare UI <=> UIManager.GetUI(Event.Shooter);
				if (UI != Null) {
					UI.StatusMessage = GetNearMissMessage(Event);
					declare LastStatusMessage for UI = 0;
					LastStatusMessage = Now;
				}
			}
			PassOn(Event);
		}
		default: {
			PassOn(Event);
		}
	}
}

// UI updates
if (G_LastUIUpdate + C_UITickInterval < Now) {
	G_LastUIUpdate = Now;
	
	// Top rankings
	if (C_UseTops) {
		declare TopNb = 2;
		if (S_FriendlyFire) {
			TopNb += 1;
		}
		declare Position = "129 -30";
		switch (((Now - StartTime) / 7000) % TopNb) {
			case 0: {
				LayerTops.ManialinkPage = GetFrameTop(T_Damage, Position);
			}
			case 1: {
				LayerTops.ManialinkPage = GetFrameTop(T_Scores, Position);
			}
			case 2: {
				LayerTops.ManialinkPage = GetFrameTop(T_Backstabber, Position);
			}
		}
	}
	
	// Clear big messages
	if (G_LastBigMessage > 0 && G_LastBigMessage + 5000 < Now) {
		UIManager.UIAll.BigMessage = "";
		G_LastBigMessage = 0;
	}
	
	// Clear status messages
	if (G_LastStatusMessage > 0 && G_LastStatusMessage + 5000 < Now) {
		UIManager.UIAll.StatusMessage = "";
		G_LastStatusMessage = 0;
	}
	
	// Update players UI
	foreach (Player in Players) {
		declare UI <=> UIManager.GetUI(Player);
		if (UI != Null) {
			// Update layers
			UpdatePositionLayer(Player);
			UpdateSimpleTeamsLayer(Player);
			
			// Update markers
			UI.Hud3dMarkers =	GetBallsHud3dMarkers()
								^
								G_ClanAttackMarkers[Player.CurrentClan]
								^
								G_ClanDefendMarkers[Player.CurrentClan];
			
			// Update crosshair extension
			if (G_BallCarryingPlayerIDs.exists(Player.Id)) {
				UI.ManialinkPage = """<label posn="0 7" halign="center" scale="2" text="{{{Teams[Player.CurrentClan-1].ColorText}}}〝     〞" style="TextCardSmallScores1"/><label posn="0 -8" halign="center" scale="1" text="{{{Teams[Player.CurrentClan-1].ColorText}}}$n$oyou have a ball" style="TextCardSmallScores2"/>""";
			} else {
				UI.ManialinkPage = "";
			}
			
			// Clear player status message
			declare LastStatusMessage for UI = 0;
			if (UI.StatusMessage != "" && (LastStatusMessage > 0 && LastStatusMessage + 4500 < Now) || UI.StatusMessage == T_WaitForSpawn) {
				UI.StatusMessage = "";
				LastStatusMessage = 0;
			}
		}
	}
	
	// Update spectators UI
	foreach (Spectator in Spectators) {
		declare UI <=> UIManager.GetUI(Spectator);
		if (UI != Null) {
			// Update layers
			UpdateSimpleTeamsLayer(Spectator);
			
			// Update markers
			UI.Hud3dMarkers = GetBallsHud3dMarkers();
		}
	}
}

// Long UI updates
if (G_LastLongUIUpdate + C_LongUITickInterval < Now) {
	G_LastLongUIUpdate = Now;
	
	// Clan scores at the top
	UpdateClanScoresSummary();
	
	// Build new simple teams layer update (to update team color)
	if (C_UseSimpleTeamsLayer) {
		SimpleLayerTeams.ManialinkPage = GetSimpleTeamsLayer();
	}
}

// Round end conditions
if (Now >= UIManager.UIAll.CountdownEndTime) {
	if (G_ClanPoints[1] == G_ClanPoints[2] && Players.count > 0) {
		// Even scores - Wait for the golden goal!
		declare Message = "$0f0$w$sGolden Goal!";
		if (UIManager.UIAll.StatusMessage != Message) {
			UIManager.UIAll.StatusMessage = Message;
		}
	} else {
		MB_StopRound = True;
		UIManager.UIAll.StatusMessage = "";
	}
}
***

***EndRound***
***
XmlRpc.SendCallback("endRound", fixGetRankings());

UIManager.UIAll.UISequence = CUIConfig::EUISequence::EndRound;

StartTime = -1;
UIManager.UIAll.CountdownEndTime = -1;
SM::UnspawnAllPlayers();
MB_Sleep(3000);

// Evaluate round
declare Message = _("Round Draw");
for (Index, 1, 2) {
	if (G_ClanPoints[Index] > G_ClanPoints[3-Index]) {
		// Team 'Index' wins the round
		ClanScores[Index] += 1;
		Message = """{{{Teams[Index-1].ColorizedName}}} wins the round!""";
		break;
	}
}
UpdateClanScoresSummary();

UIManager.UIAll.BigMessage = "";
UIManager.UIAll.SendNotice(
	Message, CUIConfig::ENoticeLevel::MatchInfo,
	Null, CUIConfig::EAvatarVariant::Default,
	CUIConfig::EUISound::EndRound, 0);
if (C_LogEnabled) MB_Log(Message);

UIManager.UIAll.ScoreTableVisibility = CUIConfig::EVisibility::ForcedVisible;

// Show all top lists
if (C_UseTops) {
	declare ManialinkPage = "";
	
	if (C_EndRoundTopsRight) {
		ManialinkPage =	GetFrameTop(T_Damage, "129 40")
						^
						GetFrameTop(T_Scores, "129 5");
		if (S_FriendlyFire) {
			ManialinkPage ^= GetFrameTop(T_Backstabber, "-129 -30");
		}
	} else {
		ManialinkPage =	GetFrameTop(T_Damage, "-65 85")
						^
						GetFrameTop(T_Scores, "-5 85");
		if (S_FriendlyFire) {
			ManialinkPage ^= GetFrameTop(T_Backstabber, "55 85");
		}
	}
	
	LayerTops.ManialinkPage = ManialinkPage;
}

// Check match end conditions
Victory::SetMatchWinnerFromScore(S_RoundsToWin, S_RoundGapToWin, S_RoundsLimit);
if (!Victory::NoMatchWinner()) {
	// Match winner found
	MB_StopMap = True;
}

// Wait to let the players take a breath
MB_Sleep(4000);

Score::RoundEnd();
Victory::RoundEnd();
BalancedWeapons::RoundEnd();
if (C_UseAirshot) {
	Airshot::RoundEnd();
}
***

***EndMap***
***
XmlRpc.SendCallback("endMap", fixGetRankings());

Score::MatchEnd();
Top::RoundEnd();
Top::MatchEnd();
Victory::MatchEnd();
BalancedWeapons::MatchEnd();
if (C_UseAirshot) {
	Airshot::MatchEnd();
}

// UI
UIManager.UIAll.BigMessage = "";
UIManager.UIAll.StatusMessage = "";
UIManager.UIAll.Hud3dMarkers = "";
UIManager.UIAll.ManialinkPage = "";
UIManager.UIAll.UISequence = CUIConfig::EUISequence::Podium;

// Hide useless layers
if (C_HideStuffAtMapEnd) {
	// Custom layers
	LayerTops.IsVisible = !C_HideStuffAtMapEnd;
	LayerTeams.IsVisible = !C_HideStuffAtMapEnd;
	SimpleLayerTeams.IsVisible = !C_HideStuffAtMapEnd;
	// Clublink layer
	if (UIManager.UIAll.UILayers.existskey(GetClublinkLayerId())) {
		UIManager.UIAll.UILayers[GetClublinkLayerId()].IsVisible = False;
	}
}

// Announce victorious clan
declare Message = _("Match Draw");
if (!Victory::IsMatchDraw()) {
	for (Index, 1, 2) {
		if (Victory::IsMatchWinner(Index)) {
			// Team 'Index' wins the match
			Message = """{{{Teams[Index-1].ColorizedName}}} wins the match!""";
			break;
		}
	}
}
UIManager.UIAll.SendNotice(
	Message, CUIConfig::ENoticeLevel::MatchInfo, 
	Null, CUIConfig::EAvatarVariant::Default, 
	CUIConfig::EUISound::EndMatch, 0);
if (C_LogEnabled) MB_Log(Message);

MB_Sleep(4000);

// Save spawned players for the next map
G_SpawnedPlayerIDs.clear();
foreach (Player in Players) {
	declare LastSpawnTime for Player = -1;
	if (LastSpawnTime >= 0) {
		G_SpawnedPlayerIDs.add(Player.Id);
	}
}

// UI Cleanup
Rules::Detach();
UIManager.UILayerDestroy(LayerTeams);
UIManager.UILayerDestroy(SimpleLayerTeams);
UIManager.UILayerDestroy(LayerPosition);
UIManager.UILayerDestroy(LayerTops);
***

***EndServer***
***
Mode::Ladder_CloseMatch();

Rules::Destroy();
Color::Unload();
UIManager.UIAll.NoticesFilter_HideMapWarning = False;

if (C_UseAchievements) {
	Achievements::ResetTop("all");
}
***

/*****************************************************
	FUNCTIONS
*****************************************************/

// Set bot power
Void SetBotPower(CSmPlayer _Player) {
	if (_Player.IsFakePlayer) {
		// Ammunition
		_Player.AmmoGain = S_BotPower;
		
		// Armor
		declare ArmorMax = MathLib::NearestInteger(S_BotPower * 100);
		if (ArmorMax > 1000) {
			ArmorMax = 1000;
		} else {
			if (ArmorMax < 100) {
				ArmorMax = 100;
			}
		}
		_Player.ArmorMax = ArmorMax;
		
		// Agressivity
		_Player.BotAgressivity = S_BotPower;
	}
}

// Spawn player
Void MySpawnPlayer(CSmPlayer _Player) {
	UnspawnPlayer(_Player);
	
	// Check if the player is allowed to spawn
	declare SpawnAllowed = False;
	declare LastSpawnTime for _Player = -1;
	if (S_MaxPlayerCount <= 0 || LastSpawnTime >= 0 || G_SpawnedPlayerIDs.exists(_Player.Id)) {
		// Player has already been spawned
		SpawnAllowed = True;
	} else {
		if (G_PlayingPlayerIDs.count < S_MaxPlayerCount && G_WaitingPlayerIDs.count > 0 && G_WaitingPlayerIDs[0] == _Player.Id) {
			// Player is the longest waiting one
			SpawnAllowed = True;
		}
	}
	
	if (SpawnAllowed) {
		// Set bot power
		SetBotPower(_Player);
		
		// Set player weapon
		BalancedWeapons::SetPlayerWeapon(_Player, True);
		if (S_LaserBattle) {
			This.SetPlayerWeapon(_Player, CSmMode::EWeapon::Laser, False);
			_Player.AmmoGain += C_InstaGibExtraAmmoGain;
			_Player.AmmoPower = C_InstaGibAmmoPower;
		} else {
			This.SetPlayerWeapon(_Player, CSmMode::EWeapon::Rocket, True);
			_Player.AmmoGain += C_NormalExtraAmmoGain;
			_Player.AmmoPower = C_NormalAmmoPower;
		}
		
		// Calculate spawn time
		declare SpawnTime = StartTime;
		if (Now > SpawnTime) {
			SpawnTime = Now + C_RespawnTime;
		}
		LastSpawnTime = SpawnTime;
		
		if (!G_PlayingPlayerIDs.exists(_Player.Id)) {
			G_PlayingPlayerIDs.add(_Player.Id);
		}
		SM::SpawnPlayer(_Player, _Player.RequestedClan, BlockSpawns[G_ClanSpawnIDs[_Player.RequestedClan]], SpawnTime);
		if (C_LogEnabled) MB_Log("""Spawning player: {{{_Player.Login}}}""");
		
		// Remove player from waiting queue
		if (G_WaitingPlayerIDs.exists(_Player.Id)) {
			declare Temp = G_WaitingPlayerIDs.remove(_Player.Id);
		}
	} else {
		// Add player to waiting queue
		if (!G_WaitingPlayerIDs.exists(_Player.Id)) {
			G_WaitingPlayerIDs.add(_Player.Id);
		}
		
		// Display waiting message
		declare UI <=> UIManager.GetUI(_Player);
		if (UI != Null) {
			if (UI.StatusMessage == "") {
				UI.StatusMessage = T_WaitForSpawn;
			}
		}
	}
}

// Set clans of spawns and poles at start of map and each round (changing sides)
Void AssignSpawnPoleClans() {
	if (C_LogEnabled) MB_Log("AssignSpawnPoleClans");
	
	// SPAWNS
	// Change sides
	declare FirstSpawns = G_ClanSpawnIDs[1];
	G_ClanSpawnIDs[1] = G_ClanSpawnIDs[2];
	G_ClanSpawnIDs[2] = FirstSpawns;
	// Assign clans
	foreach (Order => Id in G_ClanSpawnIDs) {
		switch (BlockSpawns[Id].Base.Clan) {
			case 0: {
				if (Order != 3) {
					BlockSpawns[Id].Base.Clan = Order;
				}
			}
			default: {
				BlockSpawns[Id].Base.Clan = 3-BlockSpawns[Id].Base.Clan;
			}
		}
	}
	
	// POLES
	// Change sides
	declare FirstPole = G_ClanPoleIDs[1];
	G_ClanPoleIDs[1] = G_ClanPoleIDs[2];
	G_ClanPoleIDs[2] = FirstPole;
	// Prepare poles stuff
	foreach (Order => Id in G_ClanPoleIDs) {
		// Assign clans
		switch (BlockPoles[Id].Base.Clan) {
			case 0: {
				if (Order != 3) {
					BlockPoles[Id].Base.Clan = Order;
					BlockPoles[Id].Gauge.Clan = BlockPoles[Id].Base.Clan;
				}
			}
			default: {
				BlockPoles[Id].Base.Clan = 3-BlockPoles[Id].Base.Clan;
				BlockPoles[Id].Gauge.Clan = BlockPoles[Id].Base.Clan;
			}
		}
		
		// Prepare markers
		declare PolePosi = BlockPoles[Id].Position + BlockPoles[Id].DirFront * 3.8 + <0.0, 10.0, 0.0>;
		G_ClanDefendMarkers[3-BlockPoles[Id].Base.Clan] = """<marker label="{{{Teams[BlockPoles[Id].Base.Clan-1].ColorText}}}{{{_("Defend")}}}" pos="{{{PolePosi[0]}}} {{{PolePosi[1]}}} {{{PolePosi[2]}}}"/>""";
		G_ClanAttackMarkers[BlockPoles[Id].Base.Clan] = """<marker label="{{{Teams[BlockPoles[Id].Base.Clan-1].ColorText}}}{{{_("Target")}}}" pos="{{{PolePosi[0]}}} {{{PolePosi[1]}}} {{{PolePosi[2]}}}"/>""";
	}
}

// Player picks up a ball
Void PickUpBall(Ident _PlayerId) {
	// Perform several checks if picking up is allowed right now because of weird bugs of the game itself
	if (Players.existskey(_PlayerId) && !G_BallCarryingPlayerIDs.exists(_PlayerId) && G_BallCarryingPlayerIDs.count < S_NbBalls && (Now < UIManager.UIAll.CountdownEndTime) && Now >= StartTime && Players[_PlayerId].Armor > 0 && (C_BotsCanHaveBall || !Players[_PlayerId].IsFakePlayer)) {
		G_BallCarryingPlayerIDs.add(_PlayerId);
		Score::AddPoints(Players[_PlayerId], 1);
		declare UI <=> UIManager.GetUI(Players[_PlayerId]);
		if (UI != Null) {
			UI.SendNotice(
				"", CUIConfig::ENoticeLevel::PlayerInfo, 
				Null, CUIConfig::EAvatarVariant::Default, 
				CUIConfig::EUISound::FirstHit, 0);
		}
		if (C_LogEnabled) MB_Log("""{{{Players[_PlayerId].Login}}} picked up a Ball!""");
	}
}

// Check if a player is able to score
Boolean CheckScoring(Ident _PlayerId) {
	// Check ball carrying and offzone
	if (Players.existskey(_PlayerId) && G_BallCarryingPlayerIDs.exists(_PlayerId) && Players[_PlayerId].IsInOffZone) {
		// Check distance to hoop
		declare PlayerPosi = Players[_PlayerId].Position;
		declare HoopPosi = BlockPoles[G_ClanPoleIDs[Players[_PlayerId].CurrentClan]].Position + BlockPoles[G_ClanPoleIDs[Players[_PlayerId].CurrentClan]].DirFront * 3.7;
		if (MathLib::Abs(HoopPosi[0] - PlayerPosi[0]) <= 3 && MathLib::Abs(HoopPosi[2] - PlayerPosi[2]) <= 3) {
			return True;
		}
	}
	
	return False;
}

// Ball carrier drops a ball
Void DropBall(Ident _PlayerId) {
	if (G_BallCarryingPlayerIDs.exists(_PlayerId)) {
		declare Temp = G_BallCarryingPlayerIDs.remove(_PlayerId);
	}
}

// Updates clan scores at the top
Void UpdateClanScoresSummary() {
	declare PlayerClan1Id = NullId;
	declare PlayerClan2Id = NullId;
	foreach (Player in Players) {
		if (PlayerClan1Id == NullId && Player.CurrentClan == 1) {
			PlayerClan1Id = Player.Id;
		} else {
			if (PlayerClan2Id == NullId && Player.CurrentClan == 2) {
				PlayerClan2Id = Player.Id;
			} else {
				if (PlayerClan1Id != NullId && PlayerClan2Id != NullId) {
					break;
				}
			}
		}
	}
	if (PlayerClan1Id != NullId && PlayerClan2Id != NullId) {
		UIManager.UIAll.OverlayScoreSummary = True;
		UIManager.UIAll.ScoreSummary_Player1 = PlayerClan1Id;
		UIManager.UIAll.ScoreSummary_Points1 = G_ClanPoints[Players[PlayerClan1Id].CurrentClan];
		UIManager.UIAll.ScoreSummary_MatchPoints1 = ClanScores[Players[PlayerClan1Id].CurrentClan];
		UIManager.UIAll.ScoreSummary_Player2 = PlayerClan2Id;
		UIManager.UIAll.ScoreSummary_Points2 = G_ClanPoints[Players[PlayerClan2Id].CurrentClan];
		UIManager.UIAll.ScoreSummary_MatchPoints2 = ClanScores[Players[PlayerClan2Id].CurrentClan];
	} else {
		UIManager.UIAll.OverlayScoreSummary = False;
	}
}

// Get top frame depending on style setting
Text GetFrameTop(Text _TopIdent, Text _Pos) {
	declare Title = """Top {{{C_TopCount}}} {{{_TopIdent}}}""";
	declare UnitType = "";
	switch (_TopIdent) {
		case T_Damage: {
		
		}
		case T_Scores: {
		
		}
		case T_Backstabber: {
			Title = """Flop {{{C_TopCount}}} {{{_TopIdent}}}""";
		}
	}
	if (C_UseNewTopStyle) {
		return Top::GetFrameTop2(_TopIdent, Title, _Pos, UnitType);
	}
	return Top::GetFrameTop(_TopIdent, Title, _Pos, UnitType);
}

// Create simple teams layer manialink
Text GetSimpleTeamsLayer() {
	return """
		<script><!--
			main() {
				declare Label_Blue <=> (Page.GetFirstChild("BlueLabel") as CMlLabel);
				declare Label_Red <=> (Page.GetFirstChild("RedLabel") as CMlLabel);
				declare netread Net_NbPlayersBlue for UI = 0;
				declare netread Net_ColorBlue for UI = "";
				declare netread Net_NbPlayersRed for UI = 0;
				declare netread Net_ColorRed for UI = "";
				
				while (True) {
					yield;
					
					// Update display of player counts
					// Blue
					declare LabelText = Net_NbPlayersBlue^" Player";
					if (Net_NbPlayersBlue > 1) {
						LabelText ^= "s";
					}
					Label_Blue.SetText(LabelText);
					
					// Red
					LabelText = Net_NbPlayersRed^" Player";
					if (Net_NbPlayersRed > 1) {
						LabelText ^= "s";
					}
					Label_Red.SetText(LabelText);
				}
			}
		--></script>
		<frame>
			// Blue team
			<quad posn="-32 90.2 0" sizen="15 3" halign="center" valign="top" bgcolor="{{{Color::RgbToHex(Teams[0].ColorPrimary)}}}7"/>
			<label posn="-32 90.0 1" scale="0.6" halign="center" valign="top" id="BlueLabel"/>
			
			// Red team
			<quad posn="32.5 90.2 0" sizen="15 3" halign="center" valign="top" bgcolor="{{{Color::RgbToHex(Teams[1].ColorPrimary)}}}7"/>
			<label posn="32.5 90.0 1" scale="0.6" halign="center" valign="top" id="RedLabel"/>
		</frame>""";
}

// Update simple teams layer
Void UpdateSimpleTeamsLayer(CSmPlayer _Player) {
	if (C_UseSimpleTeamsLayer) {
		declare UI <=> UIManager.GetUI(_Player);
		if (UI != Null) {
			declare netwrite Net_NbPlayersBlue for UI = 0;
			declare netwrite Net_NbPlayersRed for UI = 0;
			
			Net_NbPlayersBlue = ClansNbPlayersAlive[1];
			Net_NbPlayersRed = ClansNbPlayersAlive[2];
		}
	}
}

// Update player position layer
Void UpdatePositionLayer(CSmPlayer _Player) {
	if (C_UsePositionLayer) {
		declare UI <=> UIManager.GetUI(_Player);
		if (UI != Null) {
			declare netwrite Net_LibInterface_LayerInfoUpdate for UI = 0;
			declare netwrite Net_LibInterface_PosTotal for UI = 0;
			declare netwrite Net_LibInterface_PosCurrent for UI = 0;
			
			Net_LibInterface_LayerInfoUpdate = Now;
			Net_LibInterface_PosTotal = Scores.count;
			Net_LibInterface_PosCurrent = Scores.keyof(_Player.Score) + 1;
		}
	}
}

// Generates Hud3dMarkers for all ball
Text GetBallsHud3dMarkers() {
	declare Markers = "";
	
	// Balls on ball spawn
	if (G_BallCarryingPlayerIDs.count < S_NbBalls) {
		declare Posi = BlockPoles[G_BallsPoleId].Position;
		declare BallText = "$0f0Ball";
		if (S_NbBalls - G_BallCarryingPlayerIDs.count > 1) {
			// Multiple balls left
			BallText ^= "s";
		}
		Markers ^= """<marker label="$<{{{BallText}}}$>" pos="{{{Posi[0]}}} {{{Posi[1]}}} {{{Posi[2]}}}"/>""";
	}
	
	// Balls carried by players
	foreach (PlayerId in G_BallCarryingPlayerIDs) {
		if (Players.existskey(PlayerId)) {
			if (Players[PlayerId].IsFakePlayer) {
				// Bot
				Markers ^= """<marker label="$<{{{Teams[Players[PlayerId].CurrentClan-1].ColorText}}}Ball$>" pos="{{{Players[PlayerId].Position[0]}}} {{{Players[PlayerId].Position[1]}}} {{{Players[PlayerId].Position[2]}}}"/>""";
			} else {
				// Normal player
				Markers ^= """<marker label="$<{{{Teams[Players[PlayerId].CurrentClan-1].ColorText}}}Ball$>" playerlogin="{{{Players[PlayerId].Login}}}"/>""";
			}
		}
	}
	
	return Markers;
}

// Creates message for near miss
Text GetNearMissMessage(CSmModeEvent _Event) {
	// Catch invalid events
	if (_Event.Type != CSmModeEvent::EType::OnNearMiss) return "";	// Wrong event type
	if (_Event.MissDist <= 0.0) return "";							// Invalid miss distance
	
	// Get message text
	declare Message = "";
	declare Dist = MathLib::NearestInteger(_Event.MissDist * 1000);
	if (Dist >= 10) {
		Message = """You missed $<{{{_Event.Victim.Name}}}$> by {{{Dist/10.0}}}cm!""";
	} else {
		Dist = MathLib::NearestInteger(_Event.MissDist * 10000);
		if (Dist >= 10) {
			Message = """You missed $<{{{_Event.Victim.Name}}}$> by {{{Dist/10.0}}}mm!""";
		} else {
			Dist = MathLib::NearestInteger(_Event.MissDist * 10000000);
			Message = """You missed $<{{{_Event.Victim.Name}}}$> by {{{Dist/10.0}}}µm!""";
		}
	}
	return Message;
}

// Get the clublink layer
Ident GetClublinkLayerId() {
	if (!UIManager.UIAll.UILayers.existskey(G_ClublinkLayerId)) {
		G_ClublinkLayerId = NullId;
	}
	if (G_ClublinkLayerId == NullId) {
		foreach (Layer in UIManager.UIAll.UILayers) {
			switch (Layer.AttachId) {
				case "Unassigned": {
					// Unknown layer
					switch (Layer.Type) {
						case CUILayer::EUILayerType::ScreenIn3d: {
							// Rules layer
							Layer.AttachId = "LayerRules";
						}
						case CUILayer::EUILayerType::Normal: {
							// Clublink layer
							G_ClublinkLayerId = Layer.Id;
							break;
						}
						default: {
							// Other layers
						}
					}
				}
				default: {
					// Known layer
				}
			}
		}
	}
	return G_ClublinkLayerId;
}

// Get the current rankings for xmlrpc callbacks
Text fixGetRankings() {
	declare PlayerList = "";
	foreach (Score in Scores) {
		PlayerList ^= Score.User.Login^":"^Score.Points^";";
	}
	return PlayerList;
}
